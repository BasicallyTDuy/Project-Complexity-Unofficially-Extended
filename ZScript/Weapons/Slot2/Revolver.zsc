class Revolvermag : Ammo
{
	Default
	{
	  Inventory.Amount 0;
	  Inventory.MaxAmount 6;
	  Ammo.BackpackAmount 0;
	  Ammo.BackpackMaxAmount 6;
	  Inventory.Icon "RVICC0";
	  +INVENTORY.IGNORESKILL;
	}
}

class RevSpawner : Actor
{
	Default
	{
		+FLOORCLIP;
	}
States
    {
    Spawn:
        TNT1 A 0 Nodelay
            {
                If (ACS_NamedExecuteWithResult("WeaponSwap",3) ==2)
                {
                  A_SpawnItemEx("Revbox", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERSPECIAL, 0, tid);
                  }
                Else
                  {
                  A_SpawnItemEx("Justarevolver", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERSPECIAL, 0, tid);
                  }
            }    
        Stop;
    }
}

class MapRevSpawner : Actor
{
	Default
	{
		+FLOORCLIP;
	}
States
    {
    Spawn:
        TNT1 A 0 Nodelay
            {
                If (ACS_NamedExecuteWithResult("WeaponSwap",3) ==2)
                {
                  A_SpawnItemEx("Revbox", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERSPECIAL, 0, tid);
                  }
                Else
                  {
                  A_SpawnItemEx("JARspawner", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERSPECIAL, 0, tid);
                  }
            }    
        Stop;
    }
}

class Justarevolver : JustaWeapon
{
	Default
	{
		Weapon.AmmoType1 "Revolvermag";
		Weapon.AmmoUse1 0;
		Weapon.AmmoGive1 0;
		Weapon.AmmoType2 "Revbullets";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 6;
		Weapon.slotnumber 2;
		Weapon.SlotPriority 20;
		Weapon.SelectionOrder 1100;
		Obituary "%k had their face splattered everywhere by a large fucking bullet.";
		Weapon.UpSound "Weapon/Select";
		Inventory.PickUpSound "Weapon/Select";
		Inventory.Pickupmessage "[2] - \c[Revolvergray]Hunting revolver\c- acquired.";
		Tag "$PC_REVOLVERTAG";
		Inventory.AltHUDIcon "RVICA0";
		}
	States
	{
		Spawn:
			RVIC A -1;
			Loop;
		Select:
			REVM BA 1 A_WeaponReady(WRF_NOFIRE);
			REVO A 1 A_WeaponReady(WRF_NOFIRE);
			Goto ready;
		Deselect:
			REVO A 1;
			REVM AB 1;
			TNT1 A 0 A_lower;
			wait;
		Ready:
			REVO A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_ALLOWUSER1|WRF_ALLOWUSER2);
			Loop;
		Dryfire:
			REVO KLA 1;
			REVO A 4 A_startsound("Weapons/RevolverEmpty",11);
			Goto reload;
		Fire:
			TNT1 A 0 A_Jumpifinventory("RevolverMag",1,1);
			Goto Dryfire;
			REVO KL 1;
			REVO M 2 BRIGHT
				{
				A_GunFlash();
				A_AlertMonsters();
					If(CountInv("Power4xDamage"))
						{
						 A_startsound("Weapons/quadfire",5);
						}
					If(CountInv("Chalicedamage"))
						{
						 A_startsound("Weapons/quadfire",5);
						}					
				 A_startsound("Weapons/revolver",11);
				A_Firebullets (0.1, 0.1, -1, 100, "revolverpuff",fbf_norandom);
				}
			TNT1 A 0 
				 {
				  if(CountInv("Guntoken") && random(1,5) == 5)
					{
					A_Firebullets (3.1, 3.1, -1, 100, "revolverpuff",fbf_norandom);
					A_startsound("Gun/Proc",999,0,1);
					}
				  }
			REVO B 2 BRIGHT A_FireProjectile("GSSpawner",0,0,0,8);
			TNT1 A 0 
				 {
					if(CountInv("Ammotoken") && random(1,5) == 5) 
					{
					A_startsound("ammo/Proc",999,0,0.65);
					}
				else
					{
					A_Takeinventory("RevolverMag",1,TIF_NOTAKEINFINITE);
					}
				}
			TNT1 A 0 PC_QuakeCamera(3,2); //A_quake(3,2,0,5, "")
			REVO C 1 Offset(0,38);
			TNT1 A 0 A_JumpIf(GetCvar("Spooks_Norecoil")==1, "Followthrough");
			TNT1 A 0 PC_WeaponRecoil(4,6);
		Followthrough:
			REVO D 1 Offset(0,44);
			TNT1 A 0 A_ZoomFactor(0.99,ZOOM_INSTANT);
			REVO D 1 Offset(0,56);
			REVO E 1 Offset(0,52);
			REVO E 1 Offset(0,50);
			REVO F 1 Offset(0,47);
			TNT1 A 0 A_ZoomFactor(0.999);
			REVO G 1 Offset(0,44);
			REVO H 1 Offset(0,40);
			REVO I 1 Offset(0,36);
			REVO J 1 Offset(0,34);
			REVO A 1 Offset(0,33)A_ZoomFactor(1.0);
			TNT1 A 0 A_CheckReload;
			Goto Ready;
		AltDryfire:
			RE2F ABCA 1;
			RE2F A 4  A_startsound("Weapons/dryfire",11);
			REVO PON 1;
			Goto Reload;
		Altfire:
			REVO NOP 2;
			RE2F A 2;
			TNT1 A 0 A_Jumpifinventory("RevolverMag",1,1);
			Goto AltDryfire;
		AltHold:
			TNT1 A 0 A_Jumpifinventory("RevolverMag",1,1);
			Goto Altdryfire;
			RE2F ABC 1;
			RE2F D 2 Bright
				{
				A_GunFlash();
				A_AlertMonsters();
					If(CountInv("Power4xDamage"))
						{
						 A_startsound("Weapons/quadfire",5);
						}
					If(CountInv("Chalicedamage"))
						{
						 A_startsound("Weapons/quadfire",5);
						}					
				A_startsound("Weapons/revolver",11);
				A_Firebullets (4.5, 4.5, -1, 100,"revolverpuff",fbf_norandom);
				}
			TNT1 A 0 
				 {
				  if(CountInv("Guntoken") && random(1,5) == 5)
					{
					A_Firebullets (7.5, 7.5, -1, 100,"revolverpuff",fbf_norandom);
					A_startsound("Gun/Proc",999,0,1);
					}
				  }
			RE2F E 2 BRIGHT A_FireProjectile("GSSpawner",0,0,0,8);
			TNT1 A 0 
				 {
					if(CountInv("Ammotoken") && random(1,5) == 5) 
					{
					A_startsound("ammo/Proc",999,0,0.65);
					}
				else
					{
					A_Takeinventory("RevolverMag",1,TIF_NOTAKEINFINITE);
					}
				}
			TNT1 A 0 PC_QuakeCamera(3,2); //A_quake(3,2,0,5, "")
			RE2F F 1 Offset(0,38);
			TNT1 A 0 A_JumpIf(GetCvar("Spooks_Norecoil")==1, "Followthrough2");
			TNT1 A 0 PC_WeaponRecoil(4,6);
		Followthrough2:
			RE2F G 1 Offset(0,38) A_ZoomFactor(0.999);
			RE2F G 1 Offset(0,44);
			RE2F A 1 Offset(0,46);
			RE2F A 1 Offset(0,48) A_ZoomFactor(1.0);
			RE2F A 1 Offset(0,46) A_Refire("Althold");
			//RE2F A 1 Offset(0,44)
			RE2F A 1 Offset(0,41);
			REVO P 1 Offset(0,37);
			REVO O 1 Offset(0,35);
			REVO N 1 Offset(0,34);
			REVO A 1 Offset(0,33);
			Goto Ready;
		Flash:
			TNT1 A 2 A_Light2;
			TNT1 A 2 A_Light1;
			Goto LightDone;
		Reload:
			TNT1 A 0 A_JumpIfInventory("Revolvermag", 6, 2);
			TNT1 A 0 A_JumpIfInventory("Revbullets", 1, "ReloadWork");
			REVO A 1;
			Goto ready;
		Reloadwork:
			REVR A 1;
			REVR CF 1;
			REVR GHIJK 1;
			REVR L 2;
			REVR H 1 A_startsound("Rev/open",9);
			REVR MNO 1;
			REVR P 2;
			REVR QRS 1;
			REVR T 3  A_startsound("Rev/eject",10);
			TNT1 AAAAAA 0 A_SpawnItemEx("Magnumcasing",0,0,20,random(-1,1),random(-1,1),0,random(1,359),SXF_NOCHECKPOSITION);
			REVR WXY 1;
			REVR U 3;
			REV2 ABCD 1;
			REV2 D 10;
		ReloadLoop:
			TNT1 A 0 A_TakeInventory("Revbullets", 1);
			TNT1 A 0 A_GiveInventory("Revolvermag", 1);
			TNT1 A 0 A_JumpIfInventory("Revolvermag", 6, "ReloadFinish");
			TNT1 A 0 A_JumpIfInventory("Revbullets", 1, "ReloadLoop");
			Goto ReloadFinish;
		Reloadfinish:
			REV2 EFG 1;
			REV2 H 2;
			REV2 I 3  A_startsound("Rev/in",12);
			REV2 J 1;
			REV2 K 3;
			REV2 J 1;
			REV2 LMN 1;
			REV2 O 4;
			REV3 ABDEF 1;
			TNT1 A 0 A_startsound("Rev/close",13);
			REV3 GH 1;
			REV3 I 2;
			REV3 JKLMN 1;
			REVO A 2;
			Goto Ready;
		User1:
			TNT1 A 0 A_JumpIfInventory("HGammo",1,1);
			Goto Ready;
			REVO A 1;
			REVM AB 1;
			Goto nadethrow;
		User2:
			TNT1 A 0 A_JumpIfInventory("Mineammo",1,1);
			Goto Ready;
			REVO A 1;
			REVM AB 1;
			Goto Minethrow;
		Returnstate:
			REVM BA 1;
			REVO  A 1;
			Goto Ready;
	}
}