class ShotgunChamberLoading: Inventory{}
class ASGChamberloading : ShotgunChamberLoading {}

class Shelltube : Ammo
{
	Default
	{
	  Inventory.Amount 0;
	  Inventory.MaxAmount 10;
	  Ammo.BackpackAmount 0;
	  Ammo.BackpackMaxAmount 10;
	  Inventory.Icon "SHELLS";
	  +INVENTORY.IGNORESKILL;
  }
}

class Justashotgun : JustaWeapon
{
	Default
	{
		Weapon.AmmoType1 "Shelltube";
		Weapon.AmmoUse1 0;
		Weapon.AmmoGive1 0;
		Weapon.AmmoType2 "Shells";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 10;
		Weapon.Slotnumber 3;
		Weapon.SelectionOrder 1300;
		Weapon.SlotPriority 30;
		Obituary "%k Had their skull splattered by a hail of shot.";
		Inventory.AltHUDIcon "CSPUA0";
		Inventory.PickupSound "Weapons/PUMPB";
		Inventory.Pickupmessage "[3] - \c[brighterOrange]Combat shotgun\c- acquired.";
		Tag "$PC_SHOTGUNTAG";
	}
	
	action void PC_FireShotgun()
	{
		PC_PlayPowerModSounds();
		A_AlertMonsters();
		
		A_Startsound("Weapons/PAFire", 10, 0, 1);
		PC_FireBullets("PC_ShotgunProjectile", 9, 2.2, 0, 0, 2.2,"DoubleTube",1,1);
		PC_QuakeCamera(2,4);
		
		A_FireCustomMissile("GSSpawner2",0,0,0,4);
		A_ZoomFactor(0.99,ZOOM_INSTANT);
		
		A_GunFlash();
	}
	
	States
	{
		Spawn:
			CSPU A -1;
			Stop;
		Select:
			CSGM BA 1 A_WeaponReady(WRF_NOFIRE);
			CSGI A 1 A_WeaponReady(WRF_NOFIRE);
			Goto ready;
		Deselect:
			CSGI A 1;
			CSGM AB 1;
			TNT1 A 0 A_lower;
			wait;
		Ready:
			CSGI A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWUSER1|WRF_ALLOWUSER2|WRF_ALLOWZOOM);
			Loop;
		Dryfire:
			CSGI A 16 A_startsound("Weapons/ShotgunEempty",11);
			CSGI A 4;
			goto ready;
		Fire:
			TNT1 A 0 A_Jumpifinventory("Shelltube",1,1);
			Goto Dryfire;
			TNT1 A 0 A_JumpIfInventory("ShotgunChamberLoading",1,"ReloadCock");
			CSGF A 1 BRIGHT PC_FireShotgun();
			TNT1 A 0 A_JumpIf(GetCvar("Spooks_Norecoil")==1, "Followthrough");
			TNT1 A 0 PC_WeaponRecoil(5,2);
		Followthrough:
			CSGF B 1 BRIGHT A_FireCustomMissile("ASGSSpawner",-6,0,-19,-20,-14);
			TNT1 A 0 
				 {
					if(CountInv("Ammotoken") && random(1,5) == 5) 
					{
					A_startsound("ammo/Proc",999,0,0.65);
					}
				else
					{
					A_Takeinventory("Shelltube",1,TIF_NOTAKEINFINITE);
					}
				}
			CSGF C 1 Offset(0,33);
			CSGF D 1 Offset(0,34);
			TNT1 A 0;
			CSGI A 1 Offset(0,36);
			TNT1 A 0 A_ZoomFactor(0.999);
			CSGI A 1 Offset(0,37);
			TNT1 A 0 A_ZoomFactor(1.0);
			CSGI A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOBOB);
			Goto Ready;
		Reload:
			TNT1 A 0 A_JumpIfInventory("Shelltube", 10, 2);
			TNT1 A 0 A_JumpIfInventory("Shells", 1, "Properreload");
			CSGI A 1;
			Goto ready;
		Properreload:				
			TNT1 A 0 A_JumpIfInventory("Shelltube",1,"Properreload2");
			TNT1 A 0 A_GiveInventory("ShotgunChamberLoading",1);
			TNT1 A 0;
		Properreload2:
			CSGI A 1;
			CSGR ABCDEF 1;
			J3TR A 6;
		Reloadloop:
			J3TR B 1 A_Startsound("Weapons/shotgun/load", 10, 0, 1);
			J3TR C 1;
			J3TR D 2
				{
				A_TakeInventory("Shells",1);
				A_GiveInventory("Shelltube",1);
				}
			J3TR EFGH 1;
			J3TR A 6 A_WeaponReady(WRF_NOBOB|WRF_ALLOWUSER1|WRF_ALLOWUSER2|WRF_ALLOWZOOM);
			TNT1 A 0 A_JumpIfInventory("Shelltube",10,"ReloadFinish");
			TNT1 A 0 A_JumpIfInventory("Shells",1,"reloadloop");
			Goto ReloadFinish;
		Reloadfinish:
			J3TR A 1;
			CSGR FEDCBA 1;
			CSGI A 5;
			TNT1 A 0 A_JumpIfInventory("ShotgunChamberLoading",1,"Reloadcock");
			Goto ready;
		Reloadcock:
			CSGC ABB 1;
			CSGC C 2 A_startsound("Weapon/SABolt",11);
			CSGC D 4; //A_FireCustomMissile("ASGSSpawner",-6,0,-19,-20,-14)
			CSGC EFG 2;
			CSGI A 2;
			TNT1 A 0 A_takeinventory("ShotgunChamberloading",1);
			Goto Ready;
		Flash:
			CSGF A 1 A_Light2;
			CSGF B 1 A_Light1;
			Goto LightDone;
		User1:
			TNT1 A 0 A_JumpIfInventory("HGammo",1,1);
			Goto Ready;
			CSGI A 1;
			CSGM AB 1;
			Goto nadethrow;
		User2:
			TNT1 A 0 A_JumpIfInventory("Mineammo",1,1);
			Goto Ready;
			CSGI A 1;
			CSGM AB 1;
			Goto Minethrow;
		Returnstate:
			CSGM BA 1;
			CSGI A 1;
			Goto Ready;
	}
}