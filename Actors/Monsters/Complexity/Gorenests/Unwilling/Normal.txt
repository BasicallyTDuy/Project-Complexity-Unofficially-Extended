 ACTOR Unwillguy : Doomimp
{
	Health 120
    Speed 7
    PainChance 64
    MeleeDamage 8
	Monster
	+NOFEAR
	+Countkill
    SeeSound "ZombieFodder/sight"
    PainSound "ZombieFodder/pain"
    DeathSound "ZombieFodder/death"
    ActiveSound "ZombieFodder/active"
	HitObituary "%o was overpowered by the Unwilling."
	Obituary "%o was overpowered by the Unwilling."
	Scale 0.98
	MaxTargetRange 200
	
	Bloodcolor "Darkred"
	Bloodtype "NashGoreDefaultBlood"
	Tag "The unwilling"
	States
	{
	Spawn:
		ZFOD AABB 2
			{
			A_Look;
			A_wander;
			}
		TNT1 A 0 A_PlaySound("foot/step", 9)
		ZFOD CCDD 2
			{
			A_Look;
			A_wander;
			}
		TNT1 A 0 A_PlaySound("foot/step", 9)
		Goto see
	See:
		ZFOD AABB 2 A_Chase
		TNT1 A 0 A_Startsound("humanish/step",99,0,1)
		ZFOD CCDD 2 A_Chase
		TNT1 A 0 A_Startsound("humanish/step",99,0,1)
		Goto see
	Melee: 
		ZFOD EF 3 A_FaceTarget
		ZFOD G 4 A_custommeleeattack(10,"Imp/melee","Imp/melee")
		ZFOD FE 3 A_FaceTarget
		Goto See 
	Missile:
		TNT1 A 0 A_jump(120,"See")
	Lunge:
		TNT1 A 0 A_Jump(182,1)
		Goto see
	    TNT1 A 0 A_PlaySound("ZombieFodder/sight")
	    ZFOD EF 4 A_FaceTarget
	    TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,1)
	    TNT1 A 0 ThrustThingZ(0,20,0,1)
	    ZFOD F 2 A_Recoil(-16)
	    ZFOD F 3 A_CustomMissile("ImpLunge",10,0,0,CMF_AIMDIRECTION)
	    ZFOD G 3
	    ZFOD G 2 A_ScaleVelocity(0.2)
	    TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
	    ZFOD G 1 A_ScaleVelocity(0)
	    Goto See	
	Pain:
		ZFOD H 2
		ZFOD H 2 A_Pain
		Goto See
	Death.ice:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		ZFOD H 1
			{
			A_Dropitem("MASpawner",1,100);
			A_Dropitem("MHSpawner",1,100);
			}
		TNT1 A 0 A_Startsound("Ice/Frozeover",999,0,1)
		//TNT1 A 0 A_SpawnItemEx("Unwillfrozen",0,0,0,0,0,0,0,32)
		Stop
	Death.QSG:
		TNT1 A 0 A_jump(220,"xDeath")
	Death.SSG:
		TNT1 A 0 A_jump(180,"xDeath")
	Death.shotgun:
		TNT1 A 0 A_jump(70,"xDeath")
	Death.ASG:
		TNT1 A 0 A_jump(60,"xDeath")
	Death:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0
			{
			A_Dropitem("MASpawner",1,100);
			A_Dropitem("MHSpawner",1,100);
			}
		ZFOD J 4 A_Scream
		ZFOD K 4 A_NoBlocking
		ZFOD L 4 
		TNT1 A 0 A_JumpIf(GetCvar("Spooks_nothuds")==1, "Restofdeath")
		TNT1 A 0 A_Startsound("body/crash",97,0,1)
	RestofDeath:
		ZFOD M 1 A_fall
		ZFOD M -1 
		Stop
	Death.C4:
	XDeath:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		SLHV N 3 
			{
			A_Dropitem("MASpawner",1,100);
			A_Dropitem("MHSpawner",1,100);
			}
		TNT1 AA 0 A_CustomMissile("RedBloodCloud", random(25,50), 0, random(0,360), 2, random(0,360))
		SLHV O 3 A_XScream
		SLHV P 3
		SLHV Q 3 A_Fall
		TNT1 A 0 A_JumpIf(GetCvar("Spooks_nothuds")==1, "Restofdeath2")
		TNT1 A 0 A_Startsound("body/crash",97,0,1)
	RestofDeath2:
		SLHV RST 3
		SLHV U -1
		Stop
	Death.Massacre:
		SLHV U -1 A_NoBlocking
		Stop
	Raise:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("VileHealEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		ZFOD MLKJ 5
		Goto See
	}
}