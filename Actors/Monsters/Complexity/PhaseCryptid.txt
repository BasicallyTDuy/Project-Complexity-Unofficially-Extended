Actor PCryptidguy : Phaseguy
{
    SeeSound "PCrypt/see"
    PainSound "PCrypt/pain"
    DeathSound "PCrypt/die"
    ActiveSound "PCrypt/active"
    Obituary "%o was reduced to nothing by a Phase Cryptid's shaddowfire."
    HitObituary "%o met a Phase Cryptid intimately and paid the price."

	Tag "Phase Cryptid"
	Health 120
	
	Damagefactor "Phasefire", 0.0  
	Damagefactor "Pimpfire", 0.0
	Painchance "Cacofire", 0
	Painchance "FatsoPP", 0

DamageFactor "Phasefire", 0
	
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_giveinventory("Isphase",1)
	Idle:
		D26A A 10 A_look
		Loop
	See:
		TNT1 A 0 A_Jump(16,"GoPhase")
		TNT1 A 0 A_Jump(26,"Evade")
		D26A AABB 2 A_Chase
		TNT1 A 0 A_Startsound("humanish/step",99,0,1)
		D26A CCDD 2 A_Chase
		TNT1 A 0 A_Startsound("humanish/step",99,0,1)
		Goto see
    Evade:
		TNT1 A 0 A_Jump(128,"Evade2")
		TNT1 A 0 ThrustThing(Angle*256/360+64,16,0,0)
		D26A AAAA 3 A_Chase
		TNT1 A 0 A_Stop
		Goto See
	Evade2:
		TNT1 A 0 ThrustThing(Angle*256/360+192,16,0,0)
		D26A AAAA 3 A_Chase
		TNT1 A 0 A_Stop
		Goto See
	PhaseSee:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		D26A AABBCCDDAABBCCDDAABBCCDDAABBCCDD 1 
			{
			A_Chase("","");
			A_SpawnItemEx("GenericPhaseGhost",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERSPRITEFRAME);
			}
		D26A AABBCCDDAABBCCDD 1
			{
			A_Chase;
			A_SpawnItemEx("GenericPhaseGhost",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERSPRITEFRAME);
			}
		Goto PhaseSee+33
	GoPhase:
		TNT1 A 0 A_JumpIf(user_phased == 1,"See")
		D26A E 8
			{
			A_SetUserVar("user_phased",1);
			A_ChangeFlag("NOPAIN",1);
			A_FaceTarget;
			A_PlaySound("PhaseImp/Teleport");
			}
		D26A EEEEEEEEE 1
			{
			A_FaceTarget;
			A_SpawnItemEx("GenericPhaseGhost",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSTENCILCOL);
			A_SetRenderStyle(Alpha-0.1,STYLE_Translucent);
			}
		Goto PhaseSee
	Melee:
		TNT1 A 0 
			{
			A_SetUserVar("user_phased",0);
			A_SetRenderStyle(1.0,STYLE_Normal);
			}
        D26A EF 5 A_FaceTarget
        D26A G 5 A_CustomComboAttack("Phasefastshot", 34, 5, "imp/melee")
        D26A FE 5 A_FaceTarget			
        Goto See	
	Missile:
		TNT1 A 0 A_JumpIf(user_phased == 1,2)
		TNT1 A 0 A_Jump(64,"GoPhase")
		TNT1 A 0 
			{
			A_SetUserVar("user_phased",0);
			A_SetRenderStyle(1.0,STYLE_Normal);
			}
		TNT1 A 0 A_JumpIfCloser(150,"Lunge")
		TNT1 A 0 A_jump(100,"firespread")
		D26A EF 5 A_facetarget
		D26A G 4 A_CustomMissile("Phasefastshot", 32, 0, 0) 
		D26A EF 3 
			{
			A_facetarget;
			A_setscale(-1.0,1.0);
			}
		D26A G 4 
			{
			A_CustomMissile("Phasefastshot", 32, 0, 0);
			A_setscale(-1.0,1.0);
			}
		TNt1 A 0 A_setscale(1.0,1.0)
		Goto see
	Firespread:
		D26A EF 5 A_facetarget
		D26A G 4
			{
			A_PlaySound("imp/attack");
			A_CustomMissile("PIFBall2",32,0,0);
			A_CustomMissile("PIFBall2",32,0,6);
			A_CustomMissile("PIFBall2",32,0,-6);
			}
		TNT1 A 0 A_jump(50,"See")
		D26A EF 3 
			{
			A_facetarget;
			A_setscale(-1.0,1.0);
			}
		D26A G 4 
			{
			A_PlaySound("imp/attack");
			A_CustomMissile("PIFBall2",32,0,0);
			A_CustomMissile("PIFBall2",32,0,6);
			A_CustomMissile("PIFBall2",32,0,-6);
			A_setscale(-1.0,1.0);
			}
		TNt1 A 0 A_setscale(1.0,1.0)
		Goto see
	Lunge:
		TNT1 A 0 A_Jump(182,1)
		Goto missile+4
	    TNT1 A 0 A_PlaySound("PCrypt/see")
	    D26A EF 4 A_FaceTarget
	    TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,1)
	    TNT1 A 0 ThrustThingZ(0,20,0,1)
	    D26A F 2 A_Recoil(-16)
	    D26A F 3 A_CustomMissile("PIlunge",10,0,0,CMF_AIMDIRECTION)
	    D26A G 3
	    D26A G 2 A_ScaleVelocity(0.2)
	    TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
	    D26A G 1 A_ScaleVelocity(0)
	    Goto See
    Pain:
		TNT1 A 0 
			{
			A_SetUserVar("user_phased",0);
			A_SetRenderStyle(1.0,STYLE_Normal);
			}
        TNt1 A 0 A_setscale(1.0,1.0)
        D26A H 2 A_Pain
        Goto See
	Death.EPelletboom:
	Death.QSG:
	Death.Thorsledge:
		TNT1 A 0 A_jump(220,"xDeath")
	Death.SSG:
		TNT1 A 0 A_jump(180,"xDeath")
	Death.shotgun:
		TNT1 A 0 A_jump(70,"xDeath")
	Death.ASG:
		TNT1 A 0 A_jump(60,"xDeath")
	Death:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0 
			{
			A_SetUserVar("user_phased",0);
			A_SetRenderStyle(1.0,STYLE_Normal);
			}
		TNT1 A 0 
			{
			A_Dropitem("MASpawner",1,125);
			A_Dropitem("MHSpawner",1,125);
			A_Dropitem("Dtech",60,88);
			A_Dropitem("Staminaspawner",1,20);
			A_Dropitem("Smallcratespawner",1,10);

			A_Dropitem("PSSpawner",1,10);
			A_Dropitem("PRSpawner",1,10);
			}
        T5DF V 8 A_SetTranslucent(1)
        T5DF W 8 A_Scream
        T5DF X 6
        T5DF Y 6 A_Fall
		TNT1 A 0 A_Startsound("body/crash",97,0,1)
        T5DF Z -1
		STop
	Death.c4:
	XDeath:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0 A_SetTranslucent(1)
		TNT1 A 0 
			{
			A_Dropitem("MASpawner",1,125);
			A_Dropitem("MHSpawner",1,125);
			A_Dropitem("Dtech",60,88);
			A_Dropitem("Staminaspawner",1,20);
			A_Dropitem("Smallcratespawner",1,10);

			A_Dropitem("PSSpawner",1,30);
			A_Dropitem("PRSpawner",1,10);
			}
		TNT1 AA 0 A_CustomMissile("PurpleBloodCloud", random(25,50), 0, random(0,360), 2, random(0,360))
		D26B A 3 
		D26B B 3 A_XScream
		D26B C 3
		D26B D 3 A_Fall
		TNT1 A 0 A_Startsound("body/crash",97,0,1)
		D26B EFG 3
		D26B H -1
		Stop
	Death.Massacre:
		PIKI U -1 A_NoBlocking
		Stop
	Death.ice:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0 
			{
			A_SetUserVar("user_phased",0);
			A_SetRenderStyle(1.0,STYLE_Normal);
			}
		TNT1 A 0 A_SetTranslucent(1)
		D26B H 1
			{
			A_Dropitem("MASpawner",1,125);
			A_Dropitem("MHSpawner",1,125);
			A_Dropitem("Dtech",60,88);
			A_Dropitem("Staminaspawner",1,20);
			A_Dropitem("Smallcratespawner",1,10);

			A_Dropitem("PSSpawner",1,30);
			A_Dropitem("PRSpawner",1,10);
			}
		TNT1 A 0 A_Startsound("Ice/Frozeover",999,0,1)
		//TNT1 A 0 A_SpawnItemEx("Pcryfrozen",0,0,0,0,0,0,0,32)
		Stop
    Raise:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("VileHealEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
        VIMP LKJI 5
        Goto See
		}
}

Actor PCryptidguy2 : PCryptidguy {-Countkill}