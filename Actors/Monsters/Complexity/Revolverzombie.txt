ACTOR Revolvertoken : Inventory
{
Inventory.Amount 1
Inventory.MaxAmount 6
}

ACTOR Revolverguy : Sargentguy
{
  Health 70
  Speed 8
  PainChance 200

  SeeSound "grunt/sight"
  PainSound "grunt/pain"
  DeathSound "grunt/death"
  ActiveSound "grunt/active"
  Obituary "%o was popped between the eyes by a Revolver Zombie"
  Tag "Revolver Marine" 
  Bloodcolor "Darkred"
  Bloodtype "NashGoreDefaultBlood"
  Translation "58:66=128:136", "214:223=141:148"
  states
  {
	Spawn:
		9OS4 A 10 A_Look
		Loop
	See:
		9OS4 AABB 3 A_Chase
		TNT1 A 0 A_Startsound("foot/step",99,0,1)
		9OS4 CCDD 3 A_Chase
		TNT1 A 0 A_Startsound("foot/step",99,0,1)
		Goto see
    Missile:
		TNT1 A 0 A_JumpIfinventory("revolvertoken",6,"Reload")
		9OS4 EEEE 6 A_FaceTarget
		TNT1 A 0 A_PlaySound("Zombie/Revolverfire")
		9OS4 F 5 bright Light("Playgunlight") A_CustomMissile("Revolvertracer",28,9,frandom(-3,3),CMF_OFFSETPITCH,frandom(-1,1))
		TNT1 A 0 A_giveinventory("Revolvertoken",1)
		9OS4 E 5
		goto See
    Reload:
		TNT1 A 0 A_TakeInventory("Revolvertoken",6)
		9OS4 E 6 A_FaceTarget
		9OS4 E 12 A_PlaySound("Rev/open")
		TNT1 AAAAAA 0 A_SpawnItemEx("Magnumcasing", -8, 6, 36, Random(2, 6), 0, Random(1, 3), Random(-80, -90), 128)
		9OS4 E 1 A_PlaySound("Rev/eject")
		9OS4 E 12 A_PlaySound("Rev/in")
		9OS4 E 12 A_PlaySound("Rev/close")
		9OS4 E 6
		Goto See
    Pain:
		9OS4 G 3
		9OS4 G 3 A_Pain
		goto See
	 Death.QSG:
		TNT1 A 0 A_jump(220,"xDeath")
	 Death.ssg:
		TNT1 A 0 A_jump(180,"xDeath")
	 Death.shotgun:
		TNT1 A 0 A_jump(70,"xDeath")
	 Death.ASG:
		TNT1 A 0 A_jump(60,"xDeath")
     Death:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0 A_jumpif(GetCvar("Spooks_Dropweapon")==1,"Actualdeath")
		TNT1 A 0 
			{
			A_Dropitem("RevSpawner",1,35);
			A_dropitem("Revbullets",6,100);
			A_Dropitem("Revbox",12,20);			
			}
	Actualdeath:
		TNT1 A 0
			{
			A_dropitem("Revbullets",6,128);

			A_dropitem("HGammo",1,50);
			A_dropitem("Mineammo",1,35);
			A_dropitem("c4ammo",1,10);

			A_dropitem("ABGSpawner",1,80);
			A_dropitem("Pstimspawner",1,80);
			A_dropitem("SachSpawner",1,60);
			}
		9OS4 H 4
		9OS4 I 4 A_Scream
		9OS4 J 4 A_NoBlocking
		TNT1 A 0 A_JumpIf(GetCvar("Spooks_nothuds")==1, "Restofdeath")
		TNT1 A 0 A_Startsound("body/crash",97,0,1)
	RestofDeath:	
		POSS K 4
		POSS L -1
		Stop
	Death.C4:
	XDeath:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0 A_jumpif(GetCvar("Spooks_Dropweapon")==1,"ActualXdeath")
		TNT1 A 0 
			{
			A_Dropitem("RevSpawner",1,25);
			A_dropitem("Revbullets",6,100);
			A_Dropitem("Revbox",12,20);			
			}
	ActualXdeath:
		TNT1 A 0
			{
			A_dropitem("Revbullets",6,128);

			A_dropitem("HGammo",1,50);
			A_dropitem("Mineammo",1,35);
			A_dropitem("c4ammo",1,10);

			A_dropitem("ABGSpawner",1,80);
			A_dropitem("Pstimspawner",1,80);
			A_dropitem("SachSpawner",1,60);
			}
		TNT1 AA 0 A_CustomMissile("RedBloodCloud", random(25,50), 0, random(0,360), 2, random(0,360))
		9OS4 H 4
		POSS M 3
		POSS N 3 A_XScream
		POSS O 3 
		POSS P 3 A_Fall
		TNT1 A 0 A_JumpIf(GetCvar("Spooks_nothuds")==1, "Restofxdeath")
		TNT1 A 0 A_Startsound("body/crash",97,0,1)
	RestofxDeath:	
		POSS QRST 3
		POSS U -1
		stop
	Death.Massacre:
		RPOS W -1 A_NoBlocking
		Stop
	Death.ice:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0 A_jumpif(GetCvar("Spooks_Dropweapon")==1,"ActualIcedeath")
		TNT1 A 0 
			{
			A_Dropitem("RevSpawner",1,25);
			A_dropitem("Revbullets",6,100);
			A_Dropitem("Revbox",12,20);			
			}
	ActualIcedeath:
		TNT1 A 0
			{
			A_dropitem("Revbullets",6,128);

			A_dropitem("HGammo",1,50);
			A_dropitem("Mineammo",1,35);
			A_dropitem("c4ammo",1,10);

			A_dropitem("ABGSpawner",1,80);
			A_dropitem("Pstimspawner",1,80);
			A_dropitem("SachSpawner",1,60);
			}
		TNT1 A 0 A_Startsound("Ice/Frozeover",999,0,1)
		//TNT1 A 0 A_SpawnItemEx("FrozenRev",0,0,0,0,0,0,0,32)
		Stop
	Raise:
		POSS K 3 A_SpawnItemEx("VileHealEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		9OS4 JIH 3
		Goto See
    }
}