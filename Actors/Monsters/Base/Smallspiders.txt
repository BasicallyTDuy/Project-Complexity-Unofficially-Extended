ACTOR Dualaguy : Arachnotron
{
  Obituary "%o was vaporized by a Dualachnotron."
  Health 500
  PainChance 120

  PainChance "SoulToxic", 30
  PainChance "FatFlamer", 30
  PainChance "FiendFire", 30
  Painchance "Sledge", 100
 
  Scale 1.1
  Speed 12
  Radius 52
  Species "BabySpiders"
  DamageFactor "Baby15K", 0.0
  +MISSILEMORE
  +DONTHARMSPECIES
  +NOINFIGHTSPECIES

  SeeSound "monsters/dualachsee"
  PainSound "monsters/fusionitepain"
  DeathSound "monsters/dualachdeath"
  ActiveSound "baby/active"

  Tag "Dualachnotron"

  bloodcolor "Gold"
  Bloodtype "NashGoreBlood"
  States
  {
  Spawn:
		BSP2 A 10 A_Look
		Loop
  See:
		BSP2 A 3 A_BabyMetal
		BSP2 ABBCC 3 A_Chase
		BSP2 D 3 A_BabyMetal
		BSP2 DEEFF 3 A_Chase
		Loop
  Missile:
		BSP2 A 20 Bright A_FaceTarget
		TNT1 A 0 A_PlaySound("dualach/plasma")
		BSP2 G 2 Bright A_CustomMissile("AMK2shot",19,-12) 
		BSP2 R 2 Bright
		TNT1 A 0 A_PlaySound("dualach/plasma")
		BSP2 H 2 Bright A_CustomMissile("AMK2shot",19,12)  
		BSP2 Q 2 Bright A_SpidRefire
		Goto Missile+1
  Pain:
		BSP2 I 3
		BSP2 I 3 A_Pain
		Goto See
  Xdeath:
  Death:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0 A_jumpif(GetCvar("Spooks_Dropweapon")==1,"Actualdeath")
		TNT1 A 0 
			{
			A_Dropitem("Justapeerifle",1,20);
			A_dropitem("Plasmacell",20,50);
			}
	Actualdeath:
		TNT1 A 0
			{
			A_Dropitem("LMASpawner",1,80);
			A_Dropitem("LMHSpawner",1,80);
			}
		BSP2 I 20 A_Scream
		BSP2 J 6
		TNT1 AA 0 A_CustomMissile("YellowBloodCloud", random(25,50), 0, random(0,360), 2, random(0,360))
		BSP2 JJJJJ 2 bright
			{
				If (getcvar("Spooks_Toastermode")==1)
				{
				}
				Else
				{
				A_CustomMissile("SmokeFX2", random(20,40), 0, random(0,360), 2, random(0,360));
				A_CustomMissile("kindaSmallKaboom", random(20,40), random(10,-10), random(0,360), 2, random(0,360));
				}
			 }
		BSP2 K 6 A_NoBlocking
		BSP2 LMNO 6
		BSP2 P -1
		Stop
	Death.Massacre:
		BSP2 P -1 A_NoBlocking
		Stop
  Death.ice:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0 A_jumpif(GetCvar("Spooks_Dropweapon")==1,"Actualdeath2")
		TNT1 A 0 
			{
			A_Dropitem("Justapeerifle",1,20);
			A_dropitem("Plasmacell",20,50);
			}
	Actualdeath2:
		CSPI I 1
			{
			A_Dropitem("Justapeerifle",1,80);
			A_Dropitem("LMHSpawner",1,80);
			}
		TNT1 A 0 A_Startsound("Ice/Frozeover",999,0,1)
		//TNT1 A 0 A_SpawnItemEx("Dualfrozen",0,0,0,0,0,0,0,32)
		Stop
  Raise:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("VileHealEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		BSP2 PONMLKJ 5
		Goto See+1
  }
}

ACTOR Fusionguy : Dualaguy
{
Health 750
PainChance 100
Speed 12
Radius 52
Scale 1.1
DamageFactor "Baby15K", 0.0
Species "BabySpiders"
+MISSILEMORE

Obituary "%o was liquefied by a Fusionite."
SeeSound "baby/sight"
PainSound "monsters/fusionitepain"
DeathSound "baby/death"
ActiveSound "baby/active"

Tag "Fusionite"
Bloodcolor "Red"
Bloodtype "Nashgoreblood"

States
{
	Spawn:
		FSPI A 10 A_Look
		Loop
	See:
		FSPI A 3 A_BabyMetal
		FSPI ABBCC 3 A_Chase
		FSPI D 3 A_BabyMetal
		FSPI DEEFF 3 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_Jump(62,"Railgun")
	Plasma:
		FSPI A 15 A_FaceTarget
		FSPI Q 4 Bright A_CustomMissile("FusionShot",30,0,fRandom(-15,15),0)
		FSPI R 4 Bright A_CustomMissile("FusionShot",30,0,fRandom(-15,15),0)
		TNT1 A 0 A_Jump(12,"Railgun")
		TNT1 A 0 A_SpidRefire
		Goto Plasma+1
	Railgun:
		FSPI A 8 A_PlaySound("weapons/rgcharge")
		TNT1 A 0 A_FaceTarget
		FSPI QQQQQQQQQQQQQQQQQQQQ 1 Bright 
			{
			A_FaceTarget;
			A_CustomRailgun(0,0,"None","0000FF",RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,1,0,"RailTargetPuff",0,0,0,20,1.0,0,"none",-15);
			A_CustomRailgun(0,0,"None","0000FF",RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,1,0,"RailTargetPuff",0,0,0,20,1.0,0,"none",-15);
			}
		FSPI Q 6 Bright
		TNT1 A 0 A_PlaySound("railgunner/chargedshot")
		FSPI R 5 bright A_custommissile("fusionrail",30,0)
		FSPI R 5 Bright
		FSPI Q 35 Bright
		Goto See
	Pain:
		TNT1 A 0 A_Jump(61,"PainMissile")
		FSPI I 12 A_Pain
		Goto See
	PainMissile:
		FSPI I 12 A_Pain
		Goto Missile
	Xdeath:
	Death:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
 		TNT1 A 0 A_jumpif(GetCvar("Spooks_Dropweapon")==1,"Actualdeath")
		TNT1 A 0 
			{
			A_Dropitem("Justarailgun",1,30);
			A_dropitem("Plasmacell",20,50);
			}
	Actualdeath:
		TNT1 A 0
			{
			A_Dropitem("LMASpawner",1,110);
			A_Dropitem("LMHSpawner",1,110);
			A_Dropitem("Staminaspawner",1,10);
			}
		FSPI I 20 A_Scream
		FSPI J 6
		TNT1 AA 0 A_CustomMissile("redBloodCloud", random(25,50), 0, random(0,360), 2, random(0,360))
		FSP2 KKKKK 2 Bright 
				{
					If (getcvar("Spooks_Toastermode")==1)
					{
					}
					Else
					{
					A_CustomMissile("SmokeFX2", random(20,40), 0, random(0,360), 2, random(0,360));
					A_CustomMissile("kindaSmallKaboom", random(20,40), random(10,-10), random(0,360), 2, random(0,360));
					}
				 }
		FSPI L 6 A_NoBlocking
		FSPI MN 6
		FSPI O 6
		FSPI P -1
		Stop
	Death.Massacre:
		FSPI P -1 A_NoBlocking
		Stop
	Death.ice:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
 		TNT1 A 0 A_jumpif(GetCvar("Spooks_Dropweapon")==1,"Actualdeath2")
		TNT1 A 0 
			{
			A_Dropitem("Justarailgun",1,30);
			A_dropitem("Plasmacell",20,50);
			}
	Actualdeath2:
		FSPI I 1
			{
			A_Dropitem("LMASpawner",1,110);
			A_Dropitem("LMHSpawner",1,110);
			A_Dropitem("Staminaspawner",1,10);
			}
		TNT1 A 0 A_Startsound("Ice/Frozeover",999,0,1)
		//TNT1 A 0 A_SpawnItemEx("Fusionfrozen",0,0,0,0,0,0,0,32)
		Stop
	Raise:
		TNT1 A 0 A_SpawnItemEx("VileHealEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		FSPI ONMLKJ 6
		Goto See
    }
}


ACTOR BabyDemoguy : Dualaguy
{
  Health 1000
  PainChance 80
  Scale 1.2
  Speed 12
  Radius 52
  DamageFactor "Baby15K", 0.0
  Species "BabySpiders"
  Bloodcolor "Red"
  Bloodtype "Nashgoreblood"

 SeeSound "minidemo/sight"
  ActiveSound "minidemo/active"
  PainSound "monsters/fusionitepain"
  DeathSound "minidemo/death"
  AttackSound ""
  Obituary "%o got %p ass beat by a Baby Demolisher."
  Tag "Baby Demolisher"
  States
  {
  Spawn:
		MSPI A 10 A_Look
		Loop
  See:
		MSPI A 2 A_BabyMetal
		MSPI ABBCC 2 A_Chase
		MSPI D 2 A_BabyMetal
		MSPI DEEFF 2 A_Chase
		Loop
  Pause:
		MSPI A 10
		Goto See
  Missile:
	TNT1 A 0 A_Jump(62,"BFG15K")
  Normal:
		TNT1 A 0 A_PlaySound("monsters/darkmasterwind")
		MSPI A 15 A_FaceTarget
		TNT1 A 0 A_PlaySound("monsters/darkmasterwind2")
		MSPI Q 2 Bright Light("DualachnotronLight1")
				{
				A_SpawnItemEx("ARcasing", 12, -6, 36, Random(2, 6), 0, Random(1, 3), Random(-80, -90), 128);
				A_CustomMissile("Bullettracer",28,13,frandom(-3,3),CMF_OFFSETPITCH,frandom(-4.5,4.5));
				A_FaceTarget;
				A_PlaySoundex("Zombie/minigunfire",7);
				}
		MSPI R 2 Bright Light("DualachnotronLight1")
				{
				A_SpawnItemEx("ARcasing", 12, -6, 36, Random(2, 6), 0, Random(1, 3), Random(-80, -90), 128);
				A_CustomMissile("Bullettracer",28,13,frandom(-3,5),CMF_OFFSETPITCH,frandom(-4.5,4.5));
				A_FaceTarget;
				A_PlaySoundex("Zombie/minigunfire",7);
				}
		TNT1 A 0 A_Jump(3,"Pause","BFG15K")
		TNT1 A 0 A_SpidRefire
		Goto Normal+2
  BFG15K:
		TNT1 A 0 A_PlaySound("weapons/charge15k")
		MSPI G 18 Bright A_FaceTarget
		MSPI G 6 Bright A_FaceTarget
		MSPI H 4 Bright Light("ZombieBFG") A_CustomMissile("BabyDemo15K",20,0,Random(4,-4)) 
		MSPI G 6 Bright A_FaceTarget
		MSPI H 4 Bright Light("ZombieBFG") A_CustomMissile("BabyDemo15K",20,0,Random(4,-4)) 
		MSPI G 6 Bright A_FaceTarget	
		MSPI H 4 Bright Light("ZombieBFG") A_CustomMissile("BabyDemo15K",20,0,Random(4,-4)) 	
		MSPI G 20 Bright A_PlaySound("weapons/bfg15kcooldown",CHAN_AUTO)
		Goto See
  Pain:
		TNT1 A 0 A_stopsoundEx(7)
		TNT1 A 0 A_Jump(87,"PainMissile")
		MSPI I 12 A_Pain
		Goto See
  PainMissile:
		MSPI I 12 A_Pain
		Goto Missile
  Xdeath:
  Death:
		TNT1 A 0 A_stopsoundEx(7)
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0 A_jumpif(GetCvar("Spooks_Dropweapon")==1,"Actualdeath")
		TNT1 A 0 
			{
			A_Dropitem("MinnySpawner",1,60);
			A_Dropitem("Beltbox",100,100);
			}
	Actualdeath:
		MSPI I 20 A_Scream
		MSPI J 6
				{
				A_Dropitem("LMASpawner",1,140);
				A_Dropitem("LMHSpawner",1,140);
				}
		TNT1 AA 0 A_CustomMissile("redBloodCloud", random(25,50), 0, random(0,360), 2, random(0,360))
		MSP2 KKKKK 2 Bright
				{
					If (getcvar("Spooks_Toastermode")==1)
					{
					}
					Else
					{
					A_CustomMissile("SmokeFX2", random(20,40), 0, random(0,360), 2, random(0,360));
					A_CustomMissile("kindaSmallKaboom", random(20,40), random(10,-10), random(0,360), 2, random(0,360));
					}
				 }
    MSPI L 6 A_NoBlocking
    MSPI MN 6
    MSPI O 6
    MSPI P -1
    Stop
Death.ice:
	TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0
			 {
			  A_RemoveChildren(1,RMVF_EVERYTHING);
			  A_StopSound(5);
			  }
	MSPI I 1
			{
			A_Dropitem("LMASpawner",1,140);
			A_Dropitem("LMHSpawner",1,140);
			}
	TNT1 A 0 A_Startsound("Ice/Frozeover",999,0,1)
	//TNT1 A 0 A_SpawnItemEx("Ldemofrozen",0,0,0,0,0,0,0,32)
	Stop
  Raise:
  	TNT1 A 0
	TNT1 A 0 A_SpawnItemEx("VileHealEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    MSPI PONMLKJ 5
    Goto See+1
  }
}

ACTOR BabySentguy : Dualaguy
{
  Health 1250
  PainChance 60
  Scale 1.25
  Speed 12
  Radius 52
  Height 52
  Mass 2500
  FloatSpeed 8
  DamageFactor "Baby15K", 0.0
  Species "BabySpiders"
  
  Bloodtype "Nashgoreblood"
  Bloodcolor "red"
  -FLOAT
  -NOGRAVITY	
  +FLOORCLIP


  SeeSound "barachnophyte/sight"
  PainSound "barachnophyte/pain"
  DeathSound "barachnophyte/death"
  AttackSound ""
  Obituary "%o was not intelligent enough to take on a Baby Sentient."
  Tag "Baby Sentient"
  States
  {
  Spawn:
	TNT1 A 0 nodelay
		{
		A_SpawnItemEx("BabySentientFireSpawner",0,0,0,0,0,0,0,SXF_SETMASTER);
		A_PlaySound("monster/ambientfire",5,1.0,1);
		}
  Idle:
	 MARP AB 2 Bright A_Look
     Loop
  See:
		TNT1 A 0
			{
			A_Changeflag("Nogravity",1);
			A_ChangeFlag("FLOAT",1);
			A_ChangeFlag("FLOORCLIP",0);
			}
  Realsee:
		MARP AB 2 Bright A_Chase
		TNT1 A 0 A_Jump(12,"Evade")
		Loop
  Evade:
		TNT1 A 0 A_Jump(128,"Evade2")
		TNT1 A 0 ThrustThing(Angle*256/360+64,20,0,0)
		MARP ABABABAB 2 Bright A_Chase
		TNT1 A 0 A_Stop
		Goto realsee
  Evade2:
		TNT1 A 0 ThrustThing(Angle*256/360+192,20,0,0)
		MARP ABABABAB 2 Bright A_Chase
		TNT1 A 0 A_Stop
		Goto Realsee
  Pause:
		MARP ABAB 2 Bright
		Goto realsee
  Missile:
		TNT1 A 0 A_Jump(90,"BFG10K")
  Normal:
		TNT1 A 0 A_PlaySound("monsters/darkmasterwind")
		MARP ABABAB 2 Bright A_FaceTarget
		TNT1 A 0 A_PlaySound("monsters/darkmasterwind2")
		TNT1 A 0 A_PlaySound("sentient/fire")
		MARP K 2 Bright Light("DualachnotronLight1")
				{
				A_SpawnItemEx("SpentExpshellcasing", 12, -6, 36, Random(2, 6), 0, Random(1, 3), Random(-80, -90), 128);
				A_CustomMissile("BSETracer",28,13,frandom(-2,2),CMF_OFFSETPITCH,frandom(-2.5,2.5));
				A_FaceTarget;
				A_PlaySoundex("sentient/fire",7,1);
				}
		MARP L 2 Bright Light("DualachnotronLight1")
				{
				A_SpawnItemEx("SpentExpshellcasing", 12, -6, 36, Random(2, 6), 0, Random(1, 3), Random(-80, -90), 128);
				A_CustomMissile("BSETracer",28,13,frandom(-2,2),CMF_OFFSETPITCH,frandom(-2.5,2.5));
				A_FaceTarget;
				A_PlaySoundex("sentient/fire",7,1);
				}
		TNT1 A 0 A_Jump(13,"Pause","BFG10k")
		TNT1 A 0 A_SpidRefire
		Goto Normal+7
  BFG10K:
		TNT1 A 0 A_PlaySound("weapons/charge15k")
		MARP C 18 Bright A_FaceTarget
		MARP C 6 Bright A_FaceTarget
		MARP D 4 Bright Light("RedBFG") A_CustomMissile("BabySent10K",20,0,Random(4,-4)) 
		MARP C 6 Bright A_FaceTarget
		MARP D 4 Bright Light("RedBFG") A_CustomMissile("BabySent10K",20,0,Random(4,-4)) 
		MARP C 6 Bright A_FaceTarget	
		MARP D 4 Bright Light("RedBFG") A_CustomMissile("BabySent10K",20,0,Random(4,-4)) 	
		MARP C 20 Bright A_PlaySound("weapons/bfg15kcooldown",CHAN_AUTO)
		Goto Realsee
  Pain:
		TNT1 A 0 A_Jump(104,"PainMissile")
		MARP A 3 Bright A_Pain
		Goto Realsee
  PainMissile:
		MARP A 3 Bright A_Pain
		Goto Missile
  Death.Massacre:
		TNT1 A -1
			{
			  A_RemoveChildren(1,RMVF_EVERYTHING);
			  A_StopSound(5);
			  }
		Stop
  XDeath:
  Death:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0
			 {
			  A_RemoveChildren(1,RMVF_EVERYTHING);
			  A_XScream;
			  A_StopSound(5);
			  }
		TNT1 A 0 A_jumpif(GetCvar("Spooks_Dropweapon")==1,"Actualdeath")
		TNT1 A 0 
			{
			A_Dropitem("EminnyDrop",1,60);
			A_Dropitem("Beltbox",50,100);
			}
	Actualdeath:
		TNT1 A 0
			{
			 A_Dropitem("BMASpawner",1,120);
			 A_Dropitem("BMHSpawner",1,120);
			 A_Dropitem("Dtechcrate", 200, 88);
			 A_Dropitem("Staminaspawner",1,20);
			 A_Dropitem("Smallcratespawner",1,10);
			}
		MARP A 20 Bright A_Scream
		MARP E 6 Bright A_Fall
		MARP F 6 Bright
		TNT1 AA 0 A_CustomMissile("RedBloodCloud", random(25,50), 0, random(0,360), 2, random(0,360))
		MARP GGGGG 2 Bright
			{
				If (getcvar("Spooks_Toastermode")==1)
					{
					}
				Else
					{
					 A_CustomMissile("SmokeFX2", random(20,40), 0, random(0,360), 2, random(0,360));
					 A_CustomMissile("kindaSmallKaboom", random(20,40), random(10,-10), random(0,360), 2, random(0,360));
					}
			 }
		MARP H 6 Bright
		MARP I 4
		MARP J 2
		TNT1 A -1
		Stop
Death.ice:
	TNT1 A 0 A_GiveToTarget("KillCount",1)
	Marp A 1
			{
			 A_Dropitem("BMASpawner",1,120);
			 A_Dropitem("BMHSpawner",1,120);
			 A_Dropitem("Dtechcrate", 200, 88);
			 A_Dropitem("Staminaspawner",1,20);
			 A_Dropitem("Smallcratespawner",1,10);
			}
	TNT1 A 0 A_Startsound("Ice/Frozeover",999,0,1)
	//TNT1 A 0 A_SpawnItemEx("Lsentfrozen",0,0,0,0,0,0,0,32)
	Stop
  Raise:
  	TNT1 A 0
	TNT1 A 0 A_SpawnItemEx("VileHealEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    MARP JIHGFE 5
    Goto Flamingsee
  Flamingsee:
	TNT1 A 0
		{
		A_SpawnItemEx("BabySentientFireSpawner",0,0,0,0,0,0,0,SXF_SETMASTER);
		A_PlaySound("monster/ambientfire",5,1.0,1);
		}
	  Goto see
  }
}

//Stealth monsters
Actor StealthSSA : Dualaguy {Renderstyle Translucent  Alpha 0.5}
Actor StealthSSB : Fusionguy {Renderstyle Translucent  Alpha 0.5}
Actor StealthSSC : Babydemoguy {Renderstyle Translucent  Alpha 0.5}
Actor StealthSSD : Babysentguy {Renderstyle Translucent  Alpha 0.5}

Actor StealthJ : Randomspawner replaces StealthArachnotron
{
DropItem "StealthSSA", 256, 79
DropItem "StealthSSB", 256, 17
DropItem "StealthSSC", 256, 8
DropItem "StealthSSD", 256, 6
}
