ACTOR PhaseImpBall2 : PhaseImpBall { Decal DoomImpScorch -SEEKERMISSILE }

ACTOR Phasepoeball : PhaseImpBall { Decal DoomImpScorch -SEEKERMISSILE Damagetype "Cacocomet" }
ACTOR Phasepoeball2 : Phaseimpball { Decal DoomImpScorch Damagetype "Cacocomet" }
 
Actor PIFBall : Noblefastball
{
	+THRUGHOST
	Damage (15)
	Speed 22
	States
	{
	Spawn:
		TNT1 A 0 Nodelay A_JumpIf(GetCvar("Spooks_Toastermode")==1, "Reducedfly")
        SHAT ABC 1 Bright A_CustomMissile("Phaseballtrail",Random(-1,1),Random(-1,1))
        Goto spawn
	Reducedfly:
		SHAT ABC 1 Bright
		Goto spawn
	Death:
		TNT1 A 0 A_JumpIf(GetCvar("Spooks_Toastermode")==1, "SFXlessDTH")
        TNT1 A 0 A_SpawnItemEx("PurpleParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
		TNT1 AAAA 0 A_SpawnItemEx("Phasefire",0,0,random(10,32),random(0,2),random(0,2),8,random(0,360),SXF_TRANSFERPOINTERS|SXF_SETMASTER,0)
		TNT1 AAA 0
			{
			 A_spawnitemex("purplekaboom",0,random(15,-15),random(15,-15));
			 A_spawnitemex("SmokeFX4",0,random(15,-15),random(15,-15));
			 }
	SFXlessDTH:
    	SHAT DEFG 3 Bright
    	Stop
    }
}

Actor PIFBall2 : PIFBall
{
	+THRUGHOST
	States
	{
	Death:
		TNT1 A 0 A_JumpIf(GetCvar("Spooks_Toastermode")==1, "SFXlessDTH")
        TNT1 A 0 A_SpawnItemEx("PurpleParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
		TNT1 AAAA 0 A_SpawnItemEx("PImpfire",0,0,random(10,32),random(0,2),random(0,2),8,random(0,360),SXF_TRANSFERPOINTERS|SXF_SETMASTER,0)
		TNT1 AAA 0
			{
			 A_spawnitemex("purplekaboom",0,random(15,-15),random(15,-15));
			 A_spawnitemex("SmokeFX4",0,random(15,-15),random(15,-15));
			 }
	SFXlessDTH:
    	SHAT DEFG 3 Bright
    	Stop
    }
}



ACTOR PIlunge
{
Height 10
Radius 10
Speed 21
Damage (15)
Species "Imps"
SeeSound ""
DeathSound ""
PROJECTILE
+NOEXTREMEDEATH
+THRUSPECIES
+THRUGHOST
States
{
Spawn:
	TNT1 A 4 A_radiusgive("phasemindfuck",40, RGF_PLAYERS,1)
    Stop
XDeath:
	TNT1 A 0 A_radiusgive("phasemindfuck",40, RGF_PLAYERS,1)
	TNT1 A 0 A_PlaySound("imps/melee")
	Stop
    }
}


ACTOR PhaseImpComet : Noblecomet
{
    +DONTHARMSPECIES
	+THRUGHOST
    Species "Imps"
	Damagetype "Impcomet"
	scale 0.75
	Decal PurpleRocketScorch
	Damage (30)
	States
	{
		Spawn:
			TNT1 A 0 Nodelay A_JumpIf(GetCvar("Spooks_Toastermode")==1, "Reducedfly")
		 Standard:
			LRCB AAAABBBBCCCC 1 Bright A_SpawnItemEx("PICTrail",-10,0,0,0,0,0,0,128)
			Goto spawn
		Reducedfly:
			LRCB ABC 4 Bright
			Goto spawn
		Death:
			TNT1 A 0 A_JumpIf(GetCvar("Spooks_Toastermode")==1, "SFXlessDTH")
			TNT1 A 0 Bright 
				{
				A_Explode(34,102,0,0,82,0,0,"Apuff","Impcolor");
				A_radiusgive("phasemindfuck",80, RGF_PLAYERS,1);
				A_SpawnItemEx("purpleParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128);
				}
			LRCB DE 3 Bright
			TNT1 A 0 A_SpawnItemEx("PICDeath",0,0,0,0,0,0,0,128,0)
			Stop
		SFXlessDTH:
			TNT1 A 0 
				{
				A_Explode(34,102,0,0,28,0,0,"Apuff","Impcolor");
				A_radiusgive("phasemindfuck",80, RGF_PLAYERS,1);
				}
			LRCB DEFGHI 3 Bright
			Stop
		}
	}
	
ACTOR PICDeath
{
Projectile
RenderStyle Add
Alpha 0.7
Scale 0.75
+Nointeraction
States
{
Spawn:
	TNT1 AAA 0 Nodelay
			 {
			 A_spawnitemex("purplekaboom",0,random(15,-15),random(15,-15));
			 A_spawnitemex("SmokeFX4",0,random(15,-15),random(15,-15));
			 }
	LRCB JKLMN 3 Bright A_FadeOut(0.1)
	Stop
	}
}

Actor Phasefastshot : GuardianTracer
{
SeeSound "monsters/newfireshoot"
DeathSound "monsters/newfireexplode"
RenderStyle add
Alpha 0.85
Decal BaronScorch
-SEEKERMISSILE
+THRUGHOST
Damage (10)
Speed 24
FastSpeed 26
Damagetype "Impcomet"
States
{
	Spawn:
		PPB2 AAABBB 1 Bright A_SpawnItemEx("PhaseFStrail",0,0,0,0,0,0,0,128)
		Loop
	Death:
		 TNT1 A 0 A_JumpIf(GetCvar("Spooks_Toastermode")==1, "SFXlessDTH")
		 TNT1 A 0 A_SpawnItemEx("PurpleParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
		 TNT1 AAA 0 
			 {
			 A_spawnitemex("purplekaboom",0,random(15,-15),random(15,-15));
			 A_spawnitemex("SmokeFX4",0,random(15,-15),random(15,-15));
			 }
	SFXlessDTH:
		 TNT1 A 0 A_radiusgive("phasemindfuck",80, RGF_PLAYERS)
		 PPB2 DEFG 3 Bright
		 Stop
}}

Actor Poefastshot : Phasefastshot {Damagetype "Cacocomet" +THRUGHOST}