ACTOR GuardianTracer
{
Speed 10
Damage (25)
Radius 11
Height 8
Renderstyle Translucent
Alpha 0.85
SeeSound "skeleton/attack"
DeathSound "skeleton/tracex"
PROJECTILE
+SEEKERMISSILE
+NOEXTREMEDEATH
+THRUGHOST
+DONTSEEKINVISIBLE
Decal RevenantScorch
States
{
Spawn:
    TNT1 A 0
    TNT1 A 0 A_Jump(128,"Homing")
Normal:
    FATB AB 2 Bright
	Loop
Homing:
    TNT1 A 0 Bright A_CustomMissile("GuardianTracerSmoke",Random(2,4),Random(-2,2)) 
    FATB A 2 Bright	A_tracer2
	FATB B 2 Bright A_CustomMissile("GuardianTracerSmoke",Random(2,4),Random(-2,2))
	Loop
Death:
	TNT1 A 0 A_JumpIf(GetCvar("Spooks_Toastermode")==1, "SFXlessDTH")
    TNT1 A 0 A_SpawnItemEx("RedParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
	TNT1 AAA 0
		{
		A_spawnitemex("Kindasmallkaboom",0,random(15,-15),random(15,-15));
		A_spawnitemex("SmokeFX4",0,random(15,-15),random(15,-15));
		}
  SFXlessDTH:
    FBXP ABC 3 Bright A_SetTranslucent(0.5,1)
    Stop
    }
}

ACTOR FamineTracer
{
Speed 12
FastSpeed 16
Damage (25)
Radius 8
Height 6
Renderstyle Translucent
Alpha 0.85
Scale 0.45
SeeSound "famine/attack"
DeathSound "monsters/newfireexplode"
PROJECTILE
+SEEKERMISSILE
+DOOMBOUNCE
+DONTSPLASH
+THRUGHOST
+DONTSEEKINVISIBLE
BounceCount 2
BounceFactor 1
Decal RevenantScorch
States
{
Spawn:
    TNT1 A 0 Nodelay A_Jump(128,"Homing")
Normal:
    NREB AB 2 Bright
	Loop
Homing:
	TNT1 A 0 A_Jump(128,1,3,5,7,9)
    NREB ABABABABAB 2 Bright
	Goto StartSeek
StartSeek:
    TNT1 A 0 Bright A_SpawnItemEx("FamineTrailSmoke",0,0,6,0,0,0,0,128) 
    NREB A 2 Bright A_tracer2
	NREB B 2 Bright A_SpawnItemEx("FamineTrailSmoke",0,0,6,0,0,0,0,128) 
	Loop
Death:
	TNT1 A 0
    TNT1 A 0 A_SpawnItemEx("RedParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
	TNT1 AAA 0
		{
			If (getcvar("Spooks_Toastermode")==1)
			{
			}
			Else
			{
			A_spawnitemex("Redkaboom",0,random(15,-15),random(15,-15));
			A_CustomMissile("SmokeFX4", 0, 0, random(0,360), 2, random(0,360));	
			}
		}	
    TNT1 A 1 A_SpawnItem("FamineTracerEx")
    Stop
    }
}

ACTOR FamineTracerEx
{
RenderStyle Add
Alpha 0.75
+CLIENTSIDEONLY
+NOGRAVITY
States
{
Spawn:
	NREB CDEFGHIJKL 2 Bright
	Stop
	}
}

ACTOR FamineDashPunch : FastProjectile
{
   Height 16
   Radius 18
   Speed 50
   Damage (36)
   Species "Skeletons"
   SeeSound ""  
   DeathSound "skeleton/melee"  
   +THRUSPECIES
   States
   {
   Spawn:
    TNT1 A 2
    Stop
   Death:
    TNT1 A 1
    Stop
    }
}
