Actor dmgFloorfix: PowerProtection {DamageFactor "Slime", 0.667}

ACTOR Kicking : Inventory
{
	Inventory.MaxAmount 1
}

ACTOR Sliding : Inventory
{
	Inventory.MaxAmount 1
}

Actor PCmarine : Doomplayer 
{
	Health 100
	Gibhealth -50
	Player.DisplayName "Just a an Edgy fuck"
	Player.SoundClass "OKMarine"
	Species "Player"
	+THRUSPECIES
	Bloodtype "NashGoreBlood"
	Bloodcolor "Red"
	Player.viewheight 48
	Player.AttackZOffset 20
	Player.ForwardMove 1
	Player.sideMove 1
	Player.JumpZ 8
	Player.face STF
	Player.CrouchSprite "PLYC"
	Damagefactor "PlayerDevatravelsplash", 0.0
	Damagefactor "havocstrailplash", 0.0
	Damagefactor "EPelletboom", 0.25
	Damagefactor "Efist", 0.0
	Damagefactor "Playersledgekaboom", 0.0
	
	Player.startitem "Miniarmor"
	
	Player.startitem "Justapistol"
	Player.startitem "Justbarehands"
	Player.startitem "JustsomeC4"

	Player.startitem "Pistolclip", 15
	Player.startitem "MPistolclip", 33
	Player.startitem "SMGmag", 50
	Player.startitem "Shelltube", 10
	Player.startitem "Revolvermag", 6
	Player.startitem "Doubletube", 2
	Player.startitem "Quadtube", 4
	Player.startitem "Shellmag", 20
	Player.startitem "Riflemag", 30
	Player.startitem "BattleRiflemag", 20
	Player.startitem "Rocketmag", 6
	Player.startitem "Plasmacharge", 50
	Player.startitem "CellCharge", 5
	Player.startitem "Demoncharge", 60
	Player.startitem "Boomtube", 10
	Player.startitem "Boomertube",6
	Player.startitem "Boomtube2", 2
	Player.startitem "Deegclip", 12

	Player.startitem "Bullets", 75
	Player.startitem "HGAmmo", 2
	Player.startitem "Mineammo", 2
	Player.startitem "C4ammo", 1

	States
	{
		Spawn:
			TNT1 A 0 NoDelay 
			{
				if (G_SkillPropertyInt(SKILLP_ACSReturn) == 4)
					{ GiveInventory("dmgFloorfix",1);}
			}
			TNT1 A 0 A_Overlay(-10,"kickcheck")
		Spawnloop:
			SO2N AA 2
			Loop
		See:
			SO2N AABBCCDD 2
			Goto spawnloop
		Melee:
			TNT1 A 0 A_jumpifinventory("Weaponzoom",1,"Scopedmissile")
			TNT1 A 0 A_jumpifinventory("Meleetoken",1,"Fistmelee")
			SO22 AB 6 Bright Light("Playgunlight")
			Goto spawnloop
		Scopedmissile:
			SO2F AB 6 Bright Light("Playgunlight")
			Goto Spawnloop
		Missile:
			TNT1 A 0 A_jumpifinventory("Meleetoken",1,"Fistmelee")
			SO22 AA 6
			Goto Spawnloop
		Pain:
			TNT1 A 0 ACS_NamedLockedExecute("Painforpower",0) //Use in pain state
			TNT1 A 0 ACS_NamedLockedExecute("VoidRuneInvisibility",0)
		Pain.fire:
		Pain.slime:
			SO2H A 4 
			SO2H A 4 A_Pain
			Goto Spawnloop
		Death.slime:
			SOL2 A 5 A_StopSound(11)
			SOL2 B 5 A_startsound("Player/meltdeath",256,0,1)	
			SOL2 C 5 A_NoBlocking
			SOL2 EFG 5
			SOL2 G -1
			Stop
		Death:
			SOL2 A 5 A_StopSound(11)
			SOL2 B 5 A_PlayerScream
			SOL2 C 5 A_NoBlocking
			TNT1 A 0 A_playsound("body/crash",7)
			SOL2 EFG 5
			SOL2 G -1
			Stop
		XDeath:
			TNT1 A 0 A_SpawnItemEx("BrutalSkull",0,0,56,random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,SXF_TRANSFERTRANSLATION)
			TNT1 AAAAAAA 0 A_SpawnItemEx("Nashgoreblood",frandom(-1,1),frandom(-1,1),random(12,40),frandom(-7,7),frandom(-7,7),frandom(6,14),frandom(-18,18),SXF_TRANSFERTRANSLATION)
			TNT1 AAA 0 A_SpawnItemEx("NashGoreGibs",frandom(-1,1),frandom(-1,1),random(12,40),frandom(-7,7),frandom(-7,7),frandom(6,14),frandom(-18,18),SXF_TRANSFERTRANSLATION)
			SKGB A 1
			SKGB A 8 A_XScream
			SKGB A 5 A_NoBlocking
			TNT1 A 0 A_playsound("body/crash",7)
			SKGB BCDEF 5
			SKGB G -1
			Stop
			
		KickCheckTakeToken:
			NULL A 0 A_TakeInventory("Kicking",1)
		KickCheck:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("Sliding", 1, "SlideLoop")
			TNT1 A 1
			TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
			Loop
		DoKick:
			TNT1 A 0
			TNT1 A 0 A_OverlayFlags(-10, PSPF_ADDWEAPON, false)
			TNT1 A 0 A_OverlayOffset(-10, 0, 32)
			TNT1 A 0 A_TakeInventory("Kicking",1)
			TNT1 A 0 A_JumpIf(momZ != 0 || pitch < -25, "AirKick")
			TNT1 A 0 A_JumpIf((GetCrouchFactor < 0.7) && momx != 0 && momy != 0, "Slide") //A_JumpIfInventory("IsCrouching", 1, "SlideAttack")
			TNT1 A 0
			{
				If (countinv("Powerstrength")==1)
				{
					A_startsound("Weapons/BKickswing",40,0);
				}
				Else
				{
					A_startsound("Weapons/Kickswing",40,0);
				}
			}
			KICK N 0
			"####" A 1
			"####" BCDEFG 1
			"####" A 0
			"####" H 6
			{	
				if (CountInv("PowerStrength") == 1)
				{
					A_SpawnProjectile("SuperKickAttack", 32, 0, 0, CMF_AIMDIRECTION, 0);
					//A_CustomPunch (300, true, CPF_NOTURN, "BKickpuff", 120);
				}
				Else
				{
					A_SpawnProjectile("KickAttack", 32, 0, 0, CMF_AIMDIRECTION, 0);
					//A_CustomPunch(60, true, CPF_NOTURN, "Kickpuff", 120);
				}
			}
			"####" IGFED 1
			"####" A 0
			"####" CBA 1
			TNT1 A 0
			Goto KickCheckTakeToken
		AirKick:
			TNT1 A 0
			{
				If (countinv("Powerstrength")==1)
				{
					A_startsound("Weapons/BKickswing",40,0);
					A_Recoil(-24);
				}
				Else
				{
					A_startsound("Weapons/Kickswing",40,0);
					A_Recoil(-12);
				}
			}
			KIK4 AB 1
			KIK4 N 0
			"####" A 0
			"####" CDE 1
			"####" F 6
			{
				if (CountInv("PowerStrength") == 1)
				{
					//A_CustomPunch (300, true, CPF_NOTURN, "BKickpuff", 120);
					A_SpawnProjectile("SuperKickAttack", 0, 0, 0, CMF_AIMDIRECTION, 0);
				}
				Else
				{
					//A_CustomPunch (60, true, CPF_NOTURN, "Kickpuff", 120);
					A_SpawnProjectile("KickAttack", 0, 0, 0, CMF_AIMDIRECTION, 0);
				}
			}
			"####" GHIJKL 1
			"####" MN 1
			KIK4 OP 1
			Goto KickCheckTakeToken
		Slide:
			TNT1 A 0
			{
				//A_QuakeEx(1, 1, 1, 15, 0, 500, "", 0, 0, 0, 0, 0, 0, 1);
				A_StartSound("Player/SlideStart", 0, CHANF_OVERLAP);
				A_GiveInventory("Sliding",1);
			}
			TNT1 A 0
			SLDK ABCD 1
		SlideLoop:
			SLDK F 2
			{
				//A_ZoomFactor(0.90);
				A_SpawnProjectile("KickAttack");
				A_Recoil(-24);
			}
			TNT1 A 0 A_JumpIf(GetCrouchFactor != 0.5, "SlideEnd")
			SLDK E 3
			{
				A_SpawnProjectile("KickAttack", 16, 0, 0, CMF_AIMDIRECTION, 0);
				A_Recoil(-24);
			}
			TNT1 A 0 A_JumpIf(GetCrouchFactor != 0.5, "SlideEnd")
			SLDK F 2
			{
				A_SpawnProjectile("KickAttack", 16, 0, 0, CMF_AIMDIRECTION, 0);
				A_Recoil(-24);
			}
			TNT1 A 0 A_JumpIf(GetCrouchFactor != 0.5, "SlideEnd")
			SLDK G 3
			{
				A_SpawnProjectile("KickAttack", 16, 0, 0, CMF_AIMDIRECTION, 0);
				A_Recoil(-24);
			}
			TNT1 A 0 A_JumpIf(GetCrouchFactor != 0.5, "SlideEnd")
			SLDK F 2
			{
				A_SpawnProjectile("KickAttack", 16, 0, 0, CMF_AIMDIRECTION, 0);
				A_Recoil(-24);
			}
			TNT1 A 0 A_JumpIf(GetCrouchFactor != 0.5, "SlideEnd")
			SLDK E 3
			{
				A_SpawnProjectile("KickAttack", 16, 0, 0, CMF_AIMDIRECTION, 0);
				A_Recoil(-24);
			}
			TNT1 A 0 A_JumpIf(GetCrouchFactor != 0.5, "SlideEnd")
			TNT1 A 0 //A_JumpIf(BW_SlideLoopSlope(), "SlideLoop")
		SlideEnd:
			TNT1 A 0 A_StartSound("Player/SlideEnd", 0, CHANF_OVERLAP)
			SLDK HIJK 1
			TNT1 A 0 A_TakeInventory("Sliding",1)
			Goto KickCheckTakeToken
	}
}

ACTOR KickAttack : FastProjectile
{
	Radius 6
	Height 6
	DamageType Kick
	Projectile 
	+FORCEXYBILLBOARD
	+NOEXTREMEDEATH
	RenderStyle Add
	Alpha 0.6
    Damage (60)
    Speed 16
	AttackSound "weapons/fisthit"
	SeeSound "None"
	DeathSound "None"
	Decal Bcrack
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT
		TNT1 AA 1
		Stop
	Crash:
	XDeath:
		TNT1 A 1
		{
			A_PlaySound("weapons/fisthit");
			A_SpawnItemEx("smokeFX7", random(13,-13), random(13,-13), random(13,-13), 0.1*random(2,5), 0.1*random(2,5), 0.1*random(2,5), random(0,360), 128, 0);
		}
		TNT1 A 1
		Stop
	Death:
		TNT1 A 1
		{
			A_PlaySound("weapons/punchwall");
			A_SpawnItemEx("smokeFX7", random(13,-13), random(13,-13), random(13,-13), 0.1*random(2,5), 0.1*random(2,5), 0.1*random(2,5), random(0,360), 128, 0);
		}
		TNT1 A 1
		Stop
	}
}


ACTOR SuperKickAttack : KickAttack
{
	-NOEXTREMEDEATH
    Damage (300)
	DamageType ExtremePunches
}

actor BrutalSkull
{
    Speed 5
	Mass 6
	Scale 0.25
	BounceFactor 0.5
	BounceCount 3
	SeeSound "OKmarine/Skullbounce"
	+MISSILE
	+DOOMBOUNCE
    +CANNOTPUSH
    +DONTSPLASH
    +NOTELEPORT
    +NOBLOCKMAP
    +MOVEWITHSECTOR
    -NOGRAVITY
    -SOLID
    +CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOTONAUTOMAP
    States
    {
    Spawn:
        BGB1 ABCDEFGH 3
        loop
    Death:
        TNT1 A 0 A_Jump(255,"Death1","Death2","Death3","Death4")
	Death1:
        BGB1 A 2100
	    BGB1 AAAAAAAAAA 1 A_FadeOut(0.1)
        stop
	Death2:
        BGB1 D 2100
	    BGB1 DDDDDDDDDD 1 A_FadeOut(0.1)
        stop
	Death3:
        BGB1 J 2100
	    BGB1 JJJJJJJJJJ 1 A_FadeOut(0.1)
        stop
	Death4:
        BGB1 L 2100
	    BGB1 LLLLLLLLLL 1 A_FadeOut(0.1)
        stop
    }
}

