actor righttoken : Inventory {	inventory.maxamount 1}
actor Knucklecrack : Inventory {	inventory.maxamount 1}
Actor Meleetoken : Righttoken {}
Actor Jumpkicktoken : Righttoken {}

Actor Justbarehands : Justaweapon
{
   Weapon.SlotNumber 1
   Weapon.SlotPriority 10
   Weapon.Kickback 50
   Weapon.SelectionOrder 4000
   Inventory.PickupMessage "Dropping this might be a bad idea.."
   Obituary "%o left some teeth-marks in %K's fist."
	Tag "Remember: The First Rule(tm)."
	Inventory.AltHUDIcon "PSTRX0"
	+INVENTORY.UNDROPPABLE
	+WEAPON.WIMPY_WEAPON
	+WEAPON.MELEEWEAPON
	+WEAPON.NOAUTOFIRE
	States
	{
	Select:
		TNT1 A 0 A_giveinventory("Meleetoken",1)
		FISM BA 1 A_WeaponReady(WRF_NOFIRE)
		PUNG A 1 A_WeaponReady(WRF_NOFIRE)
		Goto ready
	Deselect:
		TNT1 A 0 A_takeinventory("Meleetoken",1)
		PUNG A 1
		FISM AB 1
		TNT1 A 0 A_lower
		wait
	Ready:
		PUNG A 1 A_WeaponReady(WRF_ALLOWUSER1|WRF_ALLOWUSER2)
		Loop
	Fire:
		TNT1 A 0 A_Jumpifinventory("righttoken",1,"Right")
		PUNG A 1
			{
				If(CountInv("Power4xDamage"))
				{
					A_startsound("Weapons/quadfire", 5);
				}
				If(CountInv("Chalicedamage"))
				{
					A_startsound("Weapons/quadfire", 5);
				}
			}
		TNT1 A 0
			{
				If (countinv("Powerstrength")==1)
				{
				A_startsound("weapons/BPunchswing",10,0);
				  }
				Else
				  {
				A_startsound("weapons/Punchswing",10);
				  }
			A_Recoil(-1 );
			}					
		PUNG BC 1
		TNT1 A 0 PC_QuakeCamera(3,5) //A_Quake(2, 2, 0, 5, "")
		PUNG D 4
			 {
				If (Countinv("Powerexplosions")==1)
					{
					A_CustomPunch (250, true, CPF_NOTURN, "Efistpuff", 70);
					}
				Else if (countinv("Powerstrength")==1)
					{
					A_CustomPunch (250, true, CPF_NOTURN, "Bfistpuff", 70);
					}
				Else
					{
					A_CustomPunch (40, true, CPF_NOTURN, "fistpuff", 70);
					}
			 }
		PUNG EFGH 1
		TNT1 A 0 A_giveinventory("Righttoken",1)
		PUNG A 1 A_WeaponReady(WRF_NOSWITCH)
		Goto ready
	Right:
		PUNS A 1
			{
				If(CountInv("Power4xDamage"))
				{
					A_startsound("Weapons/quadfire", 5);
				}
				If(CountInv("Chalicedamage"))
				{
					A_startsound("Weapons/quadfire", 5);
				}
			A_startsound("weapons/Punchswing",10);
			}
		TNT1 A 0
			{
				If (countinv("Powerstrength")==1)
				{
					A_startsound("weapons/BPunchswing",10,0);
				}
				A_Recoil(-1);
			}
		PUN2 ABC 1
		PUN2 D 0 PC_QuakeCamera(3,5) //A_Quake(2, 2, 0, 5, "")
		PUN2 D 1
			 {
				If (Countinv("Powerexplosions")==1)
					{
					A_CustomPunch (250, true, CPF_NOTURN, "Efistpuff", 70);
					}
				Else if (countinv("Powerstrength")==1)
					{
					A_CustomPunch (250, true, CPF_NOTURN, "Bfistpuff", 70);
					}
				Else
					{
					A_CustomPunch (40, true, CPF_NOTURN, "fistpuff", 70);
					}
			 }
		PUN2 E 4
		PUN2 FG 1
		PUN2 HIJK 1
		TNT1 A 0 A_takeinventory("Righttoken",1)
		PUNG A 1 A_WeaponReady(WRF_NOSWITCH)
		Goto ready
	Altfire:
		PUNG A 1
		FISM AB 1
		Goto Kicking
	User1:
		TNT1 A 0 A_JumpIfInventory("HGammo",1,1)
		Goto Ready
		PUNG A 1
		FISM AB 1
		Goto nadethrow
	User2:
		TNT1 A 0 A_JumpIfInventory("Mineammo",1,1)
		Goto Ready
		PUNG A 1
		FISM AB 1
		Goto Minethrow
	Returnstate:
		FISM BA 1
		PUNG A 1
		Goto Ready
	}
}