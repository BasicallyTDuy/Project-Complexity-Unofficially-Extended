Actor MinnySpawner
{
	+FLOORCLIP
States
    {
    Spawn:
        TNT1 A 0 Nodelay
            {
                If (ACS_NamedExecuteWithResult("WeaponSwap",4) ==2)
                {
                  A_SpawnItemEx("Beltbox", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERSPECIAL, 0, tid);
                  }
                Else
                  {
                  A_SpawnItemEx("Justaminnygun", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERSPECIAL, 0, tid);
                  }
            }    
        Stop
    }
}

Actor MapMinnyspawner
{
	+FLOORCLIP
States
    {
    Spawn:
        TNT1 A 0 Nodelay
            {
                If (ACS_NamedExecuteWithResult("WeaponSwap",4) ==2)
                {
                  A_SpawnItemEx("Beltbox", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERSPECIAL, 0, tid);
                  }
                Else
                  {
                  A_SpawnItemEx("JAMGSpawner", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERSPECIAL, 0, tid);
                  }
            }    
        Stop
    }
}


ACTOR Justaminnygun : JustaWeapon
{
    Weapon.AmmoType1 "Heavybullets"
    Weapon.AmmoUse1 0
    Weapon.AmmoGive1 100
	Weapon.slotnumber 5
	Weapon.SelectionOrder 600
	Weapon.SlotPriority 20
    Inventory.PickupSound "Weapon/Riflecock"
	Inventory.AltHUDIcon "MGUNA0"
    Obituary "%k Was penetrated more times than they could count due to their killer having a FRICKIN' MINIGUN."
	Inventory.Pickupmessage "[4] - \c[\BrighterYellow]Rotary cannon\c- acquired."
	Tag "A LOT of dakka"
    -Weapon.noautofire
	States
	{
		spawn:
			MGUN A -1
			Stop
		Select:
			MGUM BA 1 A_WeaponReady(WRF_NOFIRE)
			CHAG A 1 A_WeaponReady(WRF_NOFIRE)
			Goto ready
		Deselect:
			TNT1 A 0 
				{
				A_stopsound(10);
				A_Stopsound(12);
				}
			CHAG A 1
			MGUM AB 1
			TNT1 A 0 A_lower
			wait
		Ready:
			CHAG A 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_ALLOWUSER1|WRF_ALLOWUSER2)
			Loop
		Dryspin:
			TNT1 A 0 A_Clearrefire
			TNT1 A 0 
				{
				A_stopsound(10);
				A_Stopsound(12);
				}
			CHAG ABCD 1
			CHAG A 1 
			CHAG B 1 A_startsound("Weapons/Minnyempty",11,0)
			CHAG C 2
			CHAG D 3
			Goto Ready
		Fire:
			TNT1 A 0 A_Jumpifinventory("Heavybullets",1,1)
			Goto dryspin
			CHAG B 3 A_startsound("weapons/mgunspinstart",12,0)
			CHAG C 2
			CHAG ABC 1
			CHAG A 1 A_Refire
			CHAG B 1 A_StopSound(12)
			TNT1 A 0 A_startsound("weapons/mgunspinstop",13,0,0.5)
			CHAG C 2
			CHAG D 3
			Goto Ready
		Hold:
			TNT1 A 0 A_Jumpifinventory("Heavybullets",1,1)
			Goto dryspin
			TNT1 A 0 A_startsound("weapons/mgunspin",12,CHANF_Looping,1)
		Attackloop:
			TNT1 A 0 
				{
					If(CountInv("Power4xDamage"))
					{
						A_startsound("weapons/quadfire",5,0);
					}
					If(CountInv("Chalicedamage"))
					{
						A_startsound("weapons/quadfire",5,0);
					}				
				A_startsound("weapons/minigunfire",10,0);
				}
			TNT1 A 0 
				{
				A_Gunflash;
				A_Alertmonsters;
				}
			TNT1 A 0 PC_QuakeCamera(2,2) //Radius_Quake(2,2,0,1,0)
			CHAF A 1 Bright A_FireBullets(4.3,2.6,-1,20,"apuff",fbf_norandom)
			TNT1 A 0 
				{
				 if(CountInv("Guntoken") && random(1,5) == 5)
					{
					 A_FireBullets(7.3,5.6,-1,10,"apuff",fbf_norandom);
					A_startsound("Gun/Proc",999,0,1);
					}
				  }
			TNT1 A 0 A_ZoomFactor(0.99,ZOOM_INSTANT)
			TNT1 A 0 
				 {
					if(CountInv("Ammotoken") && random(1,5) == 5) 
					{
					A_startsound("ammo/Proc",999,0,0.65);
					}
				else
					{
					A_Takeinventory("Heavybullets",1,TIF_NOTAKEINFINITE);
					}
				}
			TNT1 A 0 A_JumpIf(GetCvar("Spooks_Norecoil")==1, "Followthrough")
			TNT1 A 0 PC_WeaponRecoil(0.75,0.5)
		Followthrough:	
			CHAF B 1 
				{
				A_FireCustomMissile("RCSpawner2",0,0,2,-3);
				A_FireCustomMissile("GSSpawner2",0,0,0,8);
				A_FireCustomMissile("Linkspawner",0,0,2,-3);
				}
			TNT1 A 0 A_ZoomFactor(0.999)
			TNT1 A 0 A_ZoomFactor(1.0)
			CHAG A 1 A_Jumpifinventory("Heavybullets",1,1)
			Goto dryspin
			TNT1 A 0 
				{
					If(CountInv("Power4xDamage"))
					{
						A_startsound("weapons/quadfire",5,0);
					}
					If(CountInv("Chalicedamage"))
					{
						A_startsound("weapons/quadfire",5,0);
					}				
				A_startsound("weapons/minigunfire",10,0);
				}
			TNT1 A 0 A_Gunflash
			TNT1 A 0 PC_QuakeCamera(2,2) //Radius_Quake(2,2,0,1,0)
			CHAF C 1 Bright A_FireBullets(4.3,2.6,-1,20,"apuff",fbf_norandom)
			TNT1 A 0
				{
				A_Takeinventory("Heavybullets",1,TIF_NOTAKEINFINITE);
				A_ZoomFactor(0.99,ZOOM_INSTANT);
				}
			TNT1 A 0 A_JumpIf(GetCvar("Spooks_Norecoil")==1, "Followthrough2")
			TNT1 A 0 PC_WeaponRecoil(0.75,0.5)
		Followthrough2:	
			CHAF D 1	
				{
				A_FireCustomMissile("RCSpawner2",0,0,2,-3);
				A_FireCustomMissile("GSSpawner2",0,0,0,8);
				A_FireCustomMissile("Linkspawner",0,0,2,-3);
				}
			TNT1 A 0 A_ZoomFactor(0.999)
		   TNT1 A 0 A_ZoomFactor(1.0)
			CHAG A 1 A_Jumpifinventory("Heavybullets",1,1)
			Goto dryspin
			CHAG A 1 A_Refire("Attackloop")
			TNT1 A 0 
				{
				A_stopsound(10);
				A_StopSound(12);
				A_clearrefire;
				}
			TNT1 A 0 A_startsound("weapons/mgunspinstop",11,0)
				TNT1 A 0 A_JumpIf(GetCvar("Spooks_Norecoil")==1, "Endingtwo")
			Endone:
				TNT1 A 0 A_startsound("weapons/mgunspinstop",13,0,0.5)
				CHAG A 1 A_SetPitch(pitch - 0.8) 	
				CHAG B 1 A_SetPitch(pitch - 0.4)
				CHAG C 1 A_SetPitch(pitch + 0.4)
				CHAG A 2 A_SetPitch(pitch + 0.8)
				CHAG BC 2 
				CHAG D 3 
				Goto Ready
			Endingtwo:
				TNT1 A 0 A_startsound("weapons/mgunspinstop",13,0,0.5)
				CHAG ABC 1
				CHAG ABC 2
				CHAG D 3
				Goto ready
		Flash:
			TNT1 A 2 A_Light2
			TNT1 A 2 A_Light1
			Goto LightDone
		Zoom:
			CHAG A 1
			MGUM AB 1
			Goto Kicking
		User1:
			TNT1 A 0 A_JumpIfInventory("HGammo",1,1)
			Goto Ready
			CHAG A 1
			MGUM AB 1
			Goto nadethrow
		User2:
			TNT1 A 0 A_JumpIfInventory("Mineammo",1,1)
			Goto Ready
			CHAG A 1
			MGUM AB 1
			Goto Minethrow
		Returnstate:
			MGUM BA 1
			CHAG A 1
			Goto Ready
	}
}
		