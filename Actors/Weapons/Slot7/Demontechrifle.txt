Actor Demoncharge : Ammo
{
  Inventory.Amount 0
  Inventory.MaxAmount 60
  Ammo.BackpackAmount 0
  Ammo.BackpackMaxAmount 60
  Inventory.Icon "CLIPA0"
  +IGNORESKILL
}

ACTOR JustaDTrifle : JustaWeapon
{
    Weapon.AmmoType1 "Demoncharge"
    Weapon.AmmoUse1 0
    Weapon.AmmoGive1 0
    Weapon.AmmoType2 "Dtech"
    Weapon.AmmoUse2 0
    Weapon.AmmoGive2 30
	Weapon.Slotnumber 7
	Weapon.SelectionOrder 300
	Weapon.SlotPriority 20
    Obituary "%k found themselves to be nothing but a pile of goop"
    Inventory.PickupSound "Weapons/DTRpickup"
	Inventory.Pickupmessage "[7] - \c[DTgreen]Demon tech rifle\c- acquired."
	Inventory.AltHUDIcon "DTEPA0"
	Tag "RNG Crutch"
	States
	{
		Spawn:
			DTEP A -1
			Stop
		Select: 
			DTRM BA 1 A_WeaponReady(WRF_NOFIRE)
			DTEK A 1 A_WeaponReady(WRF_NOFIRE)
			Goto ready
		Deselect: 
			TNT1 A 0 A_stopsound(7)
			DTEK A 1
			DTRM AB 1
			TNT1 A 0 A_lower
			wait
		Ready:
			TNT1 A 0 A_JumpIfInventory("Demoncharge",1,1)
			Goto NoBuzzing
			DTEK AAAAAAAAAA 10 A_WeaponReady(WRF_ALLOWZOOM|WRF_ALLOWRELOAD|WRF_ALLOWUSER1|WRF_ALLOWUSER2)
			TNT1 A 0 A_startsound("weapons/DTRIdle",7,0,0.45)
			DTEK BBCCDDEE 1 bright A_WeaponReady(WRF_ALLOWZOOM|WRF_ALLOWRELOAD|WRF_ALLOWUSER1|WRF_ALLOWUSER2)
			Loop
		Nobuzzing:
			DTEE A 2 A_WeaponReady(WRF_ALLOWZOOM|WRF_ALLOWRELOAD|WRF_ALLOWUSER1|WRF_ALLOWUSER2)
			Loop
		Dryfire:
			DTEE A 16 A_startsound("Weapons/DTREmpty",11,0,1)
			DTEE A 4
			goto nobuzzing
		Fire:
			 TNT1 A 0 A_jumpifinventory("Demoncharge",1,1)
			 goto dryfire
			 TNT1 A 0 
				 {
				 A_AlertMonsters;
					If(CountInv("Power4xDamage"))
						{
						A_startsound("Weapons/quadfire",5);
						}
					If(CountInv("Chalicedamage"))
						{
						A_startsound("Weapons/quadfire",5);
						}	
				 A_startsound("Weapons/DTRFire",10,0,0.6);
				 A_GunFlash;
				 }
			 DTKF A 2 Bright 
				 {
				 A_FireCustomMissile("DTP",frandom(-1,1),1,0,0,0,frandom(1,-1));
				 //Radius_Quake(4,2,0,1,0);
				 PC_QuakeCamera(2,1);
				 A_ZoomFactor(0.99,ZOOM_INSTANT);
				 }
			TNT1 A 0 
				 {
					if(CountInv("Ammotoken") && random(1,5) == 5) 
					{
					A_startsound("ammo/Proc",999,0,0.65);
					}
				else
					{
					 A_Takeinventory("Demoncharge",1,TIF_NOTAKEINFINITE);
					}
				}
			TNT1 A 0 
				 {
				  if(CountInv("Guntoken") && random(1,5) == 5)
					{
					A_FireCustomMissile("DTP",frandom(-5,5),1,0,0,0,frandom(1,-1));
					A_startsound("Gun/Proc",999,0,1);
					}
				  }
			TNT1 A 0 A_JumpIf(GetCvar("Spooks_Norecoil")==1, "Followthrough")
			TNT1 A 0 PC_WeaponRecoil(0.75,0.25)
		Followthrough:
			 DTKF Y 1 Bright 
			 TNT1 A 0 A_ZoomFactor(0.999)
			 DTKF C 2  
			 TNT1 A 0 A_ReFire
			 TNT1 A 0 A_ZoomFactor(1.0)
			 TNT1 A 0 A_jumpifinventory("Demoncharge",1,1)
			 Goto DTcoolingoff2
			 DTRC A 1 A_startsound("Weapons/DTRVent",9)
			 DTRC BC 2
			 DTRC CD 2
			 DTRC EF 2
			 DTEK A 2
			 Goto Ready
		DTcoolingoff2:
			 DTR2 A 1 A_startsound("Weapons/DTRVent",9)
			 DTR2 BCDEF 2
			 DTEE A 2
			 Goto Nobuzzing
		Flash:
			TNT1 A 2 A_Light2
			TNT1 A 2 A_Light1
			Goto LightDone
		Reload:
			TNT1 A 0 A_JumpIfInventory("Demoncharge", 60, 2)
			TNT1 A 0 A_JumpIfInventory("Dtech", 1, "ReloadWork")
			DTEK A 1
			Goto ready
		Reloadwork:
			DTEK A 1
			DTKR AB 2
			DTKR C 3
			DTKR D 4 A_startsound("Weapons/DTRMagout",11)
			TNT1 A 0 A_startsound("Weapons/DTRVent",12)
			DTKR EFG 1 Radius_Quake(2,2,0,1,0)
			DTKR H 2 Radius_Quake(2,2,0,1,0)
			DTKR I 3 Radius_Quake(2,2,0,1,0)
			DTKR J 4 A_FireCustomMissile("DemonTechEmptyMagSpawner", 0, 0, 0, -10)
			DTKR K 15 	
		Reloadloop:
			TNT1 A 0 A_TakeInventory("Dtech",1)
			TNT1 A 0 A_GiveInventory("Demoncharge",1)
			TNT1 A 0 A_JumpIfInventory("Demoncharge",60,"reloadfinish")
			TNT1 A 0 A_JumpIfInventory("Dtech",1,"Reloadloop")
			Goto Reloadfinish
		Reloadfinish:
			DTKR LM 2
			DTKR N 2 A_startsound("Weapons/DTRLoad",13)
			DTKR  O 8 
			DTKR O 2 A_startsound("Weapons/DTRMagin",14)
			DTKR O 3
			DTKR CBA 3
			DTEK A 2
			Goto Ready
		Zoom:
			TNT1 A 0 A_stopsound(7)
			DTEK A 1
			DTRM AB 1
			Goto Kicking
		User1:
			TNT1 A 0 A_JumpIfInventory("HGammo",1,1)
			Goto Ready
			TNT1 A 0 A_stopsound(7)
			DTEK A 1
			DTRM AB 1
			Goto nadethrow
		User2:
			TNT1 A 0 A_JumpIfInventory("Mineammo",1,1)
			Goto Ready
			TNT1 A 0 A_stopsound(7)
			DTEK A 1
			DTRM AB 1
			Goto Minethrow
		Returnstate:
			DTRM BA 1
			DTEK A 1
			Goto Ready
	}
}

ACTOR DTP
{
   Height 4
   Radius 4
   Speed 50
   Damage (32)
   Scale 0.06
   PROJECTILE
   +MTHRUSPECIES
   +FORCERADIUSDMG
   Species "Player"
   DeathSound "Weapons/DTRExplode"
   Damagetype "Demontech"
   Decal DTRScorch
   States
   {
   Spawn:
	  TNT1 A 0 Nodelay A_JumpIf(GetCvar("Spooks_Toastermode")==1, "Noeffectfly")
	  TNT1 A 0 A_Jump(128,"Spawn2")
   Normal:
   	  TNT1 A 0 A_Jump(188,"NormalFX")
      DTPR A 1 Bright A_SpawnItemEx("DemonTechTrail",Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-20,20),128)
      Loop
   NormalFX:
      DTPR A 1 Bright 
		  {
		  A_SpawnItemEx("DTechLightning",Random(1,-1),Random(1,-1),Random(1,-1),0,0,0,0,128);
		  A_SpawnItemEx("DemonTechTrail",Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-20,20),128);
		  }
      DTPR A 1 Bright 
		  {
		  A_SpawnItemEx("DTechLightning2",Random(1,-1),Random(1,-1),Random(1,-1),0,0,0,0,128);
		  A_SpawnItemEx("DemonTechTrail",Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-20,20),128);
		  }
      Goto Normal
   Spawn2:
	  TNT1 A 0 A_Jump(188,"Spawn2FX")
      DTPR A 1 Bright A_SpawnItemEx("DemonTechTrail2",Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-20,20),128)
      Loop
   Spawn2FX:
      DTPR A 1 Bright 
		  {
		  A_SpawnItemEx("DTechLightning",Random(1,-1),Random(1,-1),Random(1,-1),0,0,0,0,128);
		  A_SpawnItemEx("DemonTechTrail2",Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-20,20),128);
		  }
      DTPR A 1 Bright 
		  {
		  A_SpawnItemEx("DTechLightning2",Random(1,-1),Random(1,-1),Random(1,-1),0,0,0,0,128);
		  A_SpawnItemEx("DemonTechTrail2",Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-1,1),Random(-20,20),128);
		  }
      Goto Spawn2
   Noeffectfly:
	  DTPR A 1
	  Goto spawn
   Death:
   	  TNT1 A 0 A_JumpIf(GetCvar("Spooks_Toastermode")==1, "SFXlessDTH")
	  TNT1 A 0 
		  {
		  A_SpawnItemEx("GreenParticleSpawnerDT", 0, 0, 0, 0, 0, 0, 0, 128);
		  A_SpawnItemEx("DemonTechEx",0,0,0,0,0,0,0,128,0);
		  }
	  TNT1 AAA 0 
		 {
		  A_spawnitemex("greenkaboom",0,random(15,-15),random(15,-15));
		  A_spawnitemex("SmokeFX4",0,random(15,-15),random(15,-15));
		  }
	  TNT1 A 1 Bright A_Explode(20,64,1,0,44,0,0,"apuff","Demontech")
      Stop
	SFXlessDTH:
	  TNT1 A 0 A_SpawnItemEx("DemonTechEx",0,0,0,0,0,0,0,128,0)
	  TNT1 A 1 Bright A_Explode(20,64,1,0,44,0,0,"apuff","Demontech")
      Stop
	  }
}

ACTOR DemonTechEx
{
RenderStyle Add
Alpha 0.75
Scale 0.5
PROJECTILE
+NOCLIP
States
{
Spawn:
FRPG ABCDEFGH 2 Bright A_FadeOut(0.1)
Stop
}
}