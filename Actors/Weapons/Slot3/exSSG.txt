 ACTOR Boomertube : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 6
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 6
+IGNORESKILL
}

Actor Eshotguntoken : Inventory{Inventory.maxamount 1}
Actor ESSGdrop
{
	+FLOORCLIP
States
    {
    Spawn:
        TNT1 A 0 Nodelay
            {
                If (ACS_NamedExecuteWithResult("WeaponSwap",5) ==2)
                {
                  A_SpawnItemEx("Eshellbox", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERSPECIAL, 0, tid);
                  }
                Else
                  {
                  A_SpawnItemEx("ESSGPickup", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERSPECIAL, 0, tid);
                  }
            }    
        Stop
    }
}

/*Actor ESSGPickup : Custominventory
{
    Inventory.PickupSound "weapons/shellslide"
	Inventory.Pickupmessage "[3] - \c[Explosiveorange]Explosive Shotvolver\c- acquired."
	States
	{
		spawn:
			XREV Z -1
			Stop
		Pickup:
			TNT1 A 0 A_jumpifinventory("EShells",0,"Nopickup")
			TNT1 A 0
				{
				A_Giveinventory("Eshotguntoken",1);
				A_Giveinventory("JustaESSG",1);
				A_Takeinventory("JustaESG",1);
				}
			Stop
		Nopickup:
			TNT1 A 0
			Fail
	}
}*/

Actor JustaESSG : JustaWeapon
{
    Weapon.AmmoType1 "Boomertube"
    Weapon.AmmoUse1 0
    Weapon.AmmoGive1 0
    Weapon.AmmoType2 "EShells"
    Weapon.AmmoUse2 0
    Weapon.AmmoGive2 6
	Weapon.SelectionOrder 500
	Weapon.Slotnumber 3
	Weapon.SlotPriority 20
	Scale 0.9
	Obituary "%k was smeared across. . . well, everything, by a literal handcannon."
    Weapon.UpSound "Weapon/Select"
    Inventory.PickupSound "weapons/shellslide"
	Inventory.Pickupmessage "[3] - \c[Explosiveorange]Explosive Shotvolver\c- acquired."
	Tag "A literal wristbreaker"
	Inventory.AltHUDIcon "XREVZ0"
	States
		{
		spawn:
			XREV Z -1
			Stop
		Select:
			XREM BA 1 A_WeaponReady(WRF_NOFIRE)
			XREV A 1 A_WeaponReady(WRF_NOFIRE)
			Goto ready
		Deselect:
			XREV A 1
			XREM AB 1
			TNT1 A 0 A_lower
			wait
		Ready:
			XREV A 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_ALLOWRELOAD|WRF_ALLOWUSER1|WRF_ALLOWUSER2)
			Loop
		Dryfire:
			XREV ABK 1
			XREV A 16 A_startsound("Weapons/RevolverEmpty",11)
			XREV A 4
			Goto ready
		Fire:
			TNT1 A 0 A_Jumpifinventory("Boomertube",1,1)
			Goto Dryfire
			XREV B 1 A_PlaySound("Weapons/ESSGhammer", 4)
			TNT1 A 0 
				{
				A_Alertmonsters;
					If(CountInv("Power4xDamage"))
					{
						 A_startsound("Weapons/quadfire",5);
					}
					If(CountInv("Chalicedamage"))
					{
						 A_startsound("Weapons/quadfire",5);
					}
				 A_startsound("Weapons/ESSGfire",10,0,1);
				A_Gunflash;
				}	
			TNT1 A 0 A_FireBullets(3, 3, -1, 200, "ESSGPuff",FBF_NORANDOM)
			TNT1 A 0 
				 {
				  if(CountInv("Guntoken") && random(1,5) == 5)
					{
					A_FireBullets(6, 6, -1, 200, "ESSGPuff",FBF_NORANDOM);
					A_startsound("Gun/Proc",999,0,1);
					}
				  }
			XREV CD 1 BRIGHT
			TNT1 A 0 PC_QuakeCamera(2,4) //Radius_Quake(3,3,0,1,0)
			TNT1 A 0 A_ZoomFactor(0.99,ZOOM_INSTANT)
			TNT1 A 0 A_JumpIf(GetCvar("Spooks_Norecoil")==1, "Followthrough")
			TNT1 A 0 PC_WeaponRecoil(8,8)
		Followthrough:
			TNT1 A 0 A_ZoomFactor(0.999)
			XREV E 1 A_FireCustomMissile("GSSpawner",0,0,0,4)
			XREV FG 1
			TNT1 A 0 A_ZoomFactor(1.0)
			XREV H 1 A_SetPitch(+0.5 + pitch)
			XREV IJK 1 A_SetPitch(pitch+0.5)
			XREV K 4
			XREV LMN 1
			TNT1 A 0  A_startsound("Weapons/ESSGHammer",12)
			TNT1 A 0 
				 {
					if(CountInv("Ammotoken") && random(1,5) == 5) 
					{
					A_startsound("ammo/Proc",999,0,0.65);
					}
				else
					{
					A_takeinventory("boomertube",1,TIF_NOTAKEINFINITE); 
					}
				}
			XREV OPQR 1
			Goto ready
		Reload:
			TNT1 A 0 A_JumpIfInventory("Boomertube", 6, 2)
			TNT1 A 0 A_JumpIfInventory("EShells", 1, "Properreload")
			XREV A 1
			Goto Ready
		Properreload:
			XRER ABCD 1
			XRER E 5
			XRER DCBA 1
			TNT1 A 0 A_startsound("Rev/open",9)
			XRER FGH 1
			TNT1 A 0  A_startsound("Weapons/Revspin",13)
			XSPN ABCABC 1
			XRER I 4
			XRER I 20
		Reloadloop:
		    TNT1 A 0 A_playsound("weapons/shellslide",6)
			XRER JKLMNPQ 2
			TNT1 A 0 A_SpawnItemEx("SpentExpshellcasing",0,0,20,random(-1,1),random(-1,1),0,random(1,359),SXF_NOCHECKPOSITION)
			XRER Q 5
			TNT1 A 0 
				{
				A_startsound("Weapons/ExShellsert",14);
				A_TakeInventory("EShells",1);
				A_GiveInventory("Boomertube",1);
				}
			XRER RSTU 2
			TNT1 A 0 A_startsound("Weapons/Revspin",13)
			XRER VWKJI 2	
			TNT1 A 0 A_JumpIfInventory("Boomertube",6,"ReloadFinish")
			TNT1 A 0 A_JumpIfInventory("EShells",1,"Reloadloop")
			Goto ReloadFinish
		Reloadfinish:
			XRER IHG 1
			TNT1 A 0 A_startsound("Rev/close",16)
			XRER FAEDCBA 1
			XREV K 4 Offset(0,38)
			XREV LMN 1 Offset(0,38)
			TNT1 A 0 A_startsound("Weapons/ESSGHammer",18)
			XREV OPQR 1 Offset(0,38)
			XREV AA 4 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWUSER1|WRF_ALLOWUSER2)
			Goto Ready
		Zoom:
			XREV A 1
			XREM AB 1
			Goto Kicking
		User1:
			TNT1 A 0 A_JumpIfInventory("HGammo",1,1)
			Goto Ready
			XREV A 1
			XREM AB 1
			Goto nadethrow
		User2:
			TNT1 A 0 A_JumpIfInventory("Mineammo",1,1)
			Goto Ready
			XREV A 1
			XREM AB 1
			Goto Minethrow
		Returnstate:
			XREM BA 1
			XREV A 1
			Goto Ready
		}
}
			