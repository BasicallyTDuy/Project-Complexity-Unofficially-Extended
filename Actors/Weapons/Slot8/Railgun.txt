ACTOR CellCharge : Ammo 
{ 
Inventory.Amount 0 
Inventory.MaxAmount 5 
Ammo.BackpackAmount 0 
Ammo.BackpackMaxAmount 5 
+IGNORESKILL 
}

ACTOR RailgunInfrared : PowerLightAmp { Powerup.color Green 0.045 Powerup.Duration 0x7FFFFFFF }


ACTOR Justarailgun : JustaWeapon
{
    Weapon.AmmoType1 "Cellcharge"
    Weapon.AmmoUse1 0
    Weapon.AmmoGive1 0
    Weapon.AmmoType2 "Plasmacell"
    Weapon.AmmoUse2 0
	Weapon.Ammogive2 50
	Weapon.slotnumber 8
	Weapon.SelectionOrder 400
	Weapon.SlotPriority 20
    Obituary "%k found themselves to be nothing but a pile of goop"
    Inventory.PickupSound "weapons/pickuprg"
	Inventory.Pickupmessage "[8] - \c[Cellcolor]Railgun\c- acquired."
	Inventory.AltHUDIcon "SRCGA0"
	Tag "Ayy Baby wanna ride my rail?"
	States
	{
		spawn:
			SRCG A -1
			Stop
		Select: 
			RAIM BA 1 A_WeaponReady(WRF_NOFIRE)
			RAEY A 1 A_WeaponReady(WRF_NOFIRE)
			Goto ready
		Deselect: 
			TNT1 A 0 A_jumpifinventory("Weaponzoom",1,"unzoomthendeselect")
			TNT1 A 0 A_stopsound(69)
			RAEY A 1
			RAIM AB 1
			TNT1 A 0 A_lower
			wait
		Unzoomthendeselect:
			TNT1 A 0 A_takeInventory("Weaponzoom",1)
			TNT1 A 0 A_takeinventory("RailgunInfrared",1)
			TNT1 A 0 A_startsound("Weapons/RGZout",118,0,1)
			TNT1 A 0 A_SetCrosshair(0)	
			TNT1 A 0 A_ZoomFactor(1.0,Zoom_instant)
			TNT1 A 0 A_stopsound(69)
			RAIZ BA 1
			RAIM AB 1
			TNT1 A 0 A_lower
			wait
		Ready:
			TNT1 A 0 A_Jumpifinventory("Weaponzoom",1,"Readyzoom")
		Readyloop:
			TNT1 A 0 A_startsound("Weapons/Railgunhum",69,CHANF_LOOPING,0.5)
		Loopit:
			TNT1 A 0 A_jumpifinventory("Cellcharge",1,1)
			Goto Emptyready
			RAIL ABCBABCB 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_ALLOWUSER1|WRF_ALLOWUSER2)
			Loop
		Emptyready:
			TNT1 A 0 A_stopsound(69)
		Loopit2:
			RAIL V 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_ALLOWUSER1|WRF_ALLOWUSER2)
			Loop
		Readyzoom:
			TNT1 A 0 A_GiveInventory("Weaponzoom",1)
			TNT1 A 0 A_startsound("Weapons/Railgunhum",69,CHANF_LOOPING,0.5)
		Readyzoom2:
			TNT1 A 0 A_jumpifnoammo("Emptyzoom")
			SNIP AAAAAAAA 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_ALLOWUSER1|WRF_ALLOWUSER2|WRF_NOBOB)
			Loop
		EmptyZoom:
			TNT1 A 0 A_stopsound(69)
		EmptyZoomloop:
			SNIP AAAAAAAA 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_ALLOWUSER1|WRF_ALLOWUSER2|WRF_NOBOB)
			Loop
		Zoomedtemp:
			TNT1 A 0 A_startsound("Weapons/Railgunhum",69,CHANF_LOOPING,0.5)
		TempLoop:
			SNIP A 6 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_ALLOWUSER1|WRF_ALLOWUSER2|WRF_NOSECONDARY)
			Goto Readyzoom
		Unzoomedtemp:
			TNT1 A 0 A_startsound("Weapons/Railgunhum",69,CHANF_LOOPING,0.5)
		UZTL:
			 RAIL ABC 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_ALLOWUSER1|WRF_ALLOWUSER2|WRF_NOSECONDARY)
			Goto Ready
		Dryfire:
			TNT1 A 0 A_stopsound(69)
			RAIL V 16 A_startsound("Weapons/Railgunempty",11)
			RAIL V 4
			goto Emptyready
		DryfireZoom:
			SNIP A 1
				{
				A_TakeInventory("Weaponzoom",1);
				A_startsound("Weapons/ScopeZO",118,0,1);
				A_ZoomFactor(1.0,Zoom_instant);
				A_takeinventory("RailgunInfrared",1);
				}
			RAIZ BA 1
			goto Dryfire
		Fire:
			TNT1 A 0 A_JumpIfInventory("Weaponzoom",1,"FireZoom")
			TNT1 A 0 A_Jumpifinventory("Cellcharge",1,1)
			Goto dryfire
			TNT1 A 0 
				{
				A_AlertMonsters;
				PC_QuakeCamera(2,3);
				//Radius_Quake(4,3,0,1,0);
				}	
			RGFR A 2 Bright 
				{
				A_GunFlash;
						If(CountInv("Power4xDamage"))
						{
							A_PlaySound("Weapons/quadfire", 5);
						}
						If(CountInv("Chalicedamage"))
						{
							A_PlaySound("Weapons/quadfire", 5);
						}				
				A_RailAttack(300,0,1,"00 85 FF",None,RGF_FULLBRIGHT,0,"railpuff",0,0,0,40,0,0,"BlueProjParticleLarge");
				}
			TNT1 A 0 
				 {
					if(CountInv("Ammotoken") && random(1,5) == 5) 
					{
					A_startsound("ammo/Proc",999,0,0.65);
					}
				else
					{
					A_Takeinventory("Cellcharge",1,TIF_NOTAKEINFINITE);
					}
				}
			TNT1 A 0 A_ZoomFactor(0.99,ZOOM_INSTANT)
			TNT1 A 0 A_JumpIf(GetCvar("Spooks_Norecoil")==1, "Followthrough")
			TNT1 A 0 PC_WeaponRecoil(1.2,0.8)
		Followthrough:
			RGFR C 2
			RGFR DEFG 1
			TNT1 A 0 A_SetPitch(pitch + 0.6)
			RGFR H 2 A_zoomfactor(1.0)
			RGFR I 6
			RGFR JK 1 
			TNT1 A 0 A_JumpIfInventory("cellcharge",1,1)
			Goto Nomoreammo
			RGFR K 5
			RGFR KK 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
			RGFR KKKKKKKK 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
			RGFR KJ 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
			TNT1 A 0 A_startsound("weapons/Plasmabeep",999,0,1)
			2AIR ZYXXYZZZ 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
			RGFR L 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
			RAIL D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
			Goto Ready
		Nomoreammo:
			RAEY A 5
			2AIR ZYX 1
			RAIL V 9
			Goto Dryfire
		Firezoom:
			TNT1 A 0 A_Jumpifinventory("Cellcharge",1,1)
			Goto dryfireZoom
			TNT1 A 0 
				{
				A_AlertMonsters;
				PC_QuakeCamera(2,3);
				//Radius_Quake(4,3,0,1,0);
				}	
			SNIP A 1 Bright
				{
				A_GunFlash;
						If(CountInv("Power4xDamage"))
						{
							A_PlaySound("Weapons/quadfire", 5);
						}
						If(CountInv("Chalicedamage"))
						{
							A_PlaySound("Weapons/quadfire", 5);
						}	
				A_RailAttack(300,0,1,"00 85 FF",None,RGF_FULLBRIGHT,0,"railpuff",0,0,0,40,0,0,"BlueProjParticleLarge");
				}
			TNT1 A 0 
				 {
					if(CountInv("Ammotoken") && random(1,5) == 5) 
					{
					A_startsound("ammo/Proc",999,0,0.65);
					}
				else
					{
					A_Takeinventory("Cellcharge",1,TIF_NOTAKEINFINITE);
					}
				}
			TNT1 A 0 A_JumpIf(GetCvar("Spooks_Norecoil")==1, "ZoomFT")
			TNT1 A 0 A_SetPitch(pitch - 0.3)
			TNT1 A 0 A_SetPitch(pitch - 0.6)
			TNT1 A 0 A_SetPitch(pitch - 0.3)
			TNT1 A 0 A_SetPitch(pitch + 0.3)
			TNT1 A 0 A_SetPitch(pitch + 0.6)
		ZoomFT:
			SNIP A 40
			TNT1 A 0 A_JumpIfInventory("cellcharge",1,1)
			Goto EmptyZoom
			SNIP A 12 A_startsound("weapons/Plasmabeep",999,0,1)
			Goto Readyzoom	
		Flash:
			TNT1 A 2 A_Light2
			TNT1 A 2 A_Light1
			Goto LightDone	
		Reload:
			TNT1 A 0 A_Jumpifinventory("Weaponzoom",1,"Unzoom4Reload")
			TNT1 A 0 A_JumpIfInventory("Cellcharge", 5, 2)
			TNT1 A 0 A_JumpIfInventory("Plasmacell", 10, "ReloadWork")
			RAIL ABCB 2 A_WeaponReady(WRF_ALLOWZOOM|WRF_ALLOWUSER1|WRF_ALLOWUSER2|WRF_NOSECONDARY)
			Goto Ready
		Unzoom4Reload:
			TNT1 A 0 A_JumpIfInventory("Cellcharge", 5, "ReadyZoom")
			TNT1 A 0 A_takeInventory("Weaponzoom",1)
			TNT1 A 0 A_takeinventory("RailgunInfrared",1)
			TNT1 A 0 A_startsound("Weapons/RGZout",118,0,1)
			TNT1 A 0 A_SetCrosshair(0)	
			TNT1 A 0 A_ZoomFactor(1.0,Zoom_instant)
			RAIZ BA 1
			Goto Reload
		Reloadwork:
			TNT1 A 0 A_JumpIfInventory("Cellcharge", 1, 1)
			Goto Emptyreload
			RAIR ABCDEFG 1 BRIGHT
			RAIR H 1 BRIGHT 
			RAIR H 1 BRIGHT offset(0,33)
			RAIR I 1 BRIGHT offset(0,32)
			RAIR I 1 BRIGHT offset(0,33)
			RAIR J 1 BRIGHT offset(0,32)
			RAIR J 1 BRIGHT offset(0,33)
			RAIR H 1 BRIGHT offset(0,32)
			RAIR H 1 BRIGHT offset(0,33)
			RAIR K 1 BRIGHT offset(0,32)
			TNT1 A 0 
				{
				A_stopsound(69);
				A_startsound("Weapons/Railcellout",30,0);
				}
			RAIR LMNOPQRSTUVWX 1
			TNT1 A 0 A_FireCustomMissile("RailCasingSpawner",-30,0,-11,-24)
			RAIR Y 10
		Reloadloop:
			TNT1 A 0 A_TakeInventory("Plasmacell",10)
			TNT1 A 0 A_GiveInventory("Cellcharge",1)
			TNT1 A 0 A_JumpIfInventory("Cellcharge",5,"Reloadfinish")
			TNT1 A 0 A_JumpIfInventory("Plasmacell",10,"Reloadloop")
			goto reloadfinish
		Reloadfinish:
			RAIR Z 1
			2AIR ABC 1
			2AIR D 4
			2AIR EFGHI 1
			2AIR J 3 A_Startsound("Weapons/Railcellin",21,0)
			2AIR KLMNOPQRSTUV 1
			RGFR MNOPQR 1
			2AIR W 3
			2AIR XY 1
			2AIR Z 3 A_Startsound("Weapons/Railcharge",25,0)
			2AIR YX 1 
			2AIR W 8 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
			2AIR Z 1
			RAIL D 1 BRIGHT A_startsound("Weapons/RailPowered",999,0,1)
			Goto ready
		Emptyreload:
			3AIR ABCDEFG 1
			3AIR H 1  
			3AIR H 1 offset(0,33)
			3AIR I 1 offset(0,32)
			3AIR I 1 offset(0,33)
			3AIR J 1 offset(0,32)
			3AIR J 1 offset(0,33)
			3AIR H 1 offset(0,32)
			3AIR H 1 offset(0,33)
			3AIR K 1 offset(0,32)
			TNT1 A 0 A_startsound("Weapons/Railcellout",30,0)
			RAIR LMNOPQRSTUVWX 1
			TNT1 A 0 A_FireCustomMissile("RailCasingSpawner",-30,0,-11,-24)
			RAIR Y 10
			Goto reloadloop
		Altfire:
			TNT1 A 0 A_JumpIfInventory("Weaponzoom",1,"UnZoom")
			TNT1 A 0 A_GiveInventory("Weaponzoom",1)
			TNT1 A 0 A_SetCrosshair(8)
			RAIZ AB 1
			TNT1 A 0 A_startsound("Weapons/RGZIn",120,0)	
			TNT1 A 0 A_ZoomFactor(4.0,Zoom_instant)
			TNT1 A 0 A_giveinventory("RailgunInfrared",1)
			Goto Zoomedtemp
		Unzoom:
			TNT1 A 0 A_takeInventory("Weaponzoom",1)
			TNT1 A 0 A_takeinventory("RailgunInfrared",1)
			TNT1 A 0 A_startsound("Weapons/RGZout",118,0,1)
			TNT1 A 0 A_SetCrosshair(0)	
			TNT1 A 0 A_ZoomFactor(1.0,Zoom_instant)
			RAIZ BA 1
			Goto UnZoomedtemp
		Zoom:
			TNT1 A 0 A_Jumpifinventory("Weaponzoom",1,"Unzoom4Kick") 
			TNT1 A 0 A_stopsound(69)
			RAEY A 1
			RAIM AB 1
			Goto Kicking
		Unzoom4Kick:
			TNT1 A 0 A_takeInventory("Weaponzoom",1)
			TNT1 A 0 A_takeinventory("RailgunInfrared",1)
			TNT1 A 0 A_startsound("Weapons/RGZout",118,0,1)
			TNT1 A 0 A_SetCrosshair(0)	
			TNT1 A 0 A_ZoomFactor(1.0,Zoom_instant)
			RAIZ BA 1
			RAIM AB 1
			Goto Kicking
		User1:
			TNT1 A 0 A_JumpIfInventory("HGammo",1,1)
			Goto Ready
			TNT1 A 0 A_Jumpifinventory("Weaponzoom",1,"Unzoom4Nade") 
			TNT1 A 0 A_stopsound(69)
			RAEY A 1
			RAIM AB 1
			Goto nadethrow
		Unzoom4Nade:
			TNT1 A 0 A_JumpIfInventory("HGammo",1,1)
			Goto Ready
			TNT1 A 0 A_takeInventory("Weaponzoom",1)
			TNT1 A 0 A_takeinventory("RailgunInfrared",1)
			TNT1 A 0 A_startsound("Weapons/RGZout",118,0,1)
			TNT1 A 0 A_SetCrosshair(0)	
			TNT1 A 0 A_ZoomFactor(1.0,Zoom_instant)
			RAIZ BA 1
			RAIM AB 1
			Goto nadethrow
		User2:
			TNT1 A 0 A_JumpIfInventory("Mineammo",1,1)
			Goto Ready
			TNT1 A 0 A_Jumpifinventory("Weaponzoom",1,"Unzoom4Mine")
			TNT1 A 0 A_stopsound(69)
			RAEY A 1
			RAIM AB 1
			Goto Minethrow
		Unzoom4Mine:
			TNT1 A 0 A_JumpIfInventory("HGammo",1,1)
			Goto Ready
			TNT1 A 0 A_takeInventory("Weaponzoom",1)
			TNT1 A 0 A_takeinventory("RailgunInfrared",1)
			TNT1 A 0 A_startsound("Weapons/RGZout",118,0,1)
			TNT1 A 0 A_SetCrosshair(0)	
			TNT1 A 0 A_ZoomFactor(1.0,Zoom_instant)
			RAIZ BA 1
			RAIM AB 1
			Goto Minethrow
		Returnstate:
			RAIM BA 1
			RAEY A 1
			Goto Ready
	}
}

//Test stuff
ACTOR BaseProjectileParticleLarge
{
  +NoInteraction
  +ClientSideOnly
  +ForceXYBillboard
  RenderStyle Add
  Scale 0.1
  +FLATSPRITE
  States
  {
  Spawn:
	TNT1 A 2
	TNT1 A 1 Bright
	Goto Spawn2
  Spawn2:
	"####" "#" 1 Bright A_FadeOut(0.1)
	"####" "#" 0 A_SetScale(ScaleX-0.004)
    Loop
  }
}

ACTOR BlueProjParticleLarge : BaseProjectileParticleLarge
{
  States
  {
  Spawn:
	TNT1 A 2
	CLBA C 1 Bright
	Goto Spawn2
  }
}

ACTOR GreenProjParticleLarge : BaseProjectileParticleLarge
{
  Scale 0.12
  States
  {
  Spawn:
	TNT1 A 2
	CLBA H 1 Bright
	Goto Spawn2
  }
}