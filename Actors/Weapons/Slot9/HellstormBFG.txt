Actor Screamercounter : Inventory {Inventory.maxamount 20 }

actor Screamertokenthing : CustomInventory
{
    -INVBAR
    +INVENTORY.UNDROPPABLE
    +INVENTORY.AUTOACTIVATE
    Inventory.maxamount 40
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("Screamercounter",10,"Clear")
        TNT1 A 0 A_GiveInventory("Screamercounter",1)
        TNT1 A 15
        stop
      Clear:
        TNT1 A 0 A_TakeInventory("Screamercounter",10)
		TNT1 A 0 
				 {
					if(CountInv("Ammotoken") && random(1,5) == 5) 
					{
					A_startsound("ammo/Proc",999,0,0.65);
					}
				else
					{
					A_takeinventory("Devacell",1,TIF_NOTAKEINFINITE);
					}
				}		
        TNT1 A 15
        stop
    }
}


ACTOR JustaScreamer : JustaWeapon
{
    Weapon.AmmoType1 "Devacell"
    Weapon.AmmoUse1 0
    Weapon.AmmoGive1 4
	Weapon.SelectionOrder 4000
	Weapon.Slotnumber 9
    Obituary "%o was Evaporated by %k's havoc screamer."
    Weapon.UpSound "Weapon/Select"
	Inventory.PickupSound "Weapons/Havokpickup"
	Inventory.Pickupmessage "[9] - \c[\megaarmorred]Havoc Screamer\c- acquired."
	Tag " This thing sure does look spooky."
	Inventory.AltHUDIcon "HFG2A0"
	States
		{
		Spawn:
				HFG2 A -1
				Stop
		Select: 
				DTSM BA 1 A_WeaponReady(WRF_NOFIRE)
				DTSG A 1 A_WeaponReady(WRF_NOFIRE)
				Goto ready
		Deselect:
				TNT1 A 0 
					{
					A_Stopsound(666);
					A_stopsound(10);
					}
				DTSG A 1
				DTSM AB 1
				TNT1 A 0 A_lower
				wait
		Ready:
				TNT1 A 0 A_jumpifnoammo("Emptyready")
		Actualready:
				TNT1 A 0 A_startsound("Weapons/Havokidle",666,0,0.5)
				DTSG AAABBBCCCDDDEEEDDDCCCBBBAAA 3 A_WeaponReady(WRF_ALLOWZOOM|WRF_ALLOWUSER1|WRF_ALLOWUSER2)
				Loop
		Emptyready:
				TNT1 A 0 A_JumpIfInventory("Devacell",1,"Ready")
				DTSG A 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_ALLOWUSER1|WRF_ALLOWUSER2)
				Loop
		Dryfire:
				DTSG A 16 A_startsound("Weapons/click3",11)
				DTSG A 4
				goto Ready
		Cooldowndryfire:
				TNT1 A 0 A_stopsound(10)
				TNT1 A 0 A_startsound("Weapons/Havokcooldown",20)
				DTSG AAAAAAAAAAAAAAAAAAAA 1 A_FireCustomMissile("GSSpawner",0,0,0,8)
				DTSG A 16 
				DTSG A 4 A_startsound("Weapons/click3",11)
				goto Ready
		Fire:
				TNT1 A 0 A_Jumpifinventory("Devacell",1,1)
				Goto Dryfire
				TNT1 A 0 A_stopsound(666)
				DTSG EF 3
				DTSG G 2
				DTSG H 1 Bright 
					{
					A_AlertMonsters;
					A_startsound("Weapons/Havokcharge",9);
					A_Quake(2,28,0,1,"none");
					}
				TNT1 A 0 A_refire
				DTSG G 2
				DTSG FE 3
				Goto Cooldown+2
		Hold:
				TNT1 A 0 A_Jumpifinventory("Devacell",1,1)
				Goto Cooldowndryfire
				TNT1 A 0 A_playsound("Weapons/Havokbeamstart",chan_weapon)
				DTSG I 15 Bright
		Fireloop:
				TNT1 A 0 A_Jumpifinventory("Devacell",1,1)
				Goto Cooldowndryfire
				DTSG J 1 Bright 
					{
					A_GunFlash;
							If(CountInv("Power4xDamage"))
							{
								A_startsound("Weapons/quadfire",5);
							}
							If(CountInv("Chalicedamage"))
							{
								A_startsound("Weapons/quadfire",5);
							}	
					A_startsound("Weapons/HavokBeamloop",10,CHANF_LOOPING);
					A_FireCustomMissile("Havocbeam",FRandom(0.1,-0.1),1,0,0,0,FRandom(0.1,-0.1));
					PC_QuakeCamera(3,5);
					//A_Quake(1,6,0,1,"none");
					A_giveinventory("Screamertokenthing",1);
					A_Recoil(0.1);
					}
				TNT1 A 0 A_JumpIf(GetCvar("Spooks_Norecoil")==1, "Followthrough")
				TNT1 A 0 A_SetAngle(frandom(0.3, -0.3) + angle)
				TNT1 A 0 A_SetPitch(-0.2 + pitch)
		  Followthrough:
				TNT1 A 0 A_JumpIfNoAmmo("Cooldowndryfire")
				DTSG JJJJ 1 A_Refire("FireLoop")
				Goto Cooldown
		  CoolDown:
				TNT1 A 0 A_stopsound(10)
				TNT1 A 0 A_startsound("Weapons/Havokbeamstop",12)
				DTSG JIHGFEDCBA 2
				TNT1 A 0 A_startsound("Weapons/havokcooldown",13)
				DTSG AAAAAAAAAAAAAAAAAAAA 1 A_FireCustomMissile("GSSpawner",0,0,0,8)
				Goto Ready
		Zoom:
				TNT1 A 0 
					{
					A_Stopsound(666);
					A_stopsound(10);
					}
				DTSG A 1
				DTSM AB 1
				Goto Kicking
		User1:
				TNT1 A 0 A_JumpIfInventory("HGammo",1,1)
				Goto Ready
				TNT1 A 0 
					{
					A_Stopsound(666);
					A_stopsound(10);
					}
				DTSG A 1
				DTSM AB 1
				Goto nadethrow
		User2:
				TNT1 A 0 A_JumpIfInventory("Mineammo",1,1)
				Goto Ready
				TNT1 A 0 
					{
					A_Stopsound(666);
					A_stopsound(10);
					}
				DTSG A 1
				DTSM AB 1
				Goto Minethrow
		Returnstate:
				DTSM BA 1
				DTSG E 1
				Goto Ready
	}
}


//The Beam
Actor Havocbeam : FRipperProjectile
{
  Speed 200
  Radius 13
  Height 8
  Damage 30
  Renderstyle Add
  Damagetype "Havoc"
  SeeSound ""
  Scale 0.45
  Species "Player"
  +THRUSPECIES
  -CANNOTPUSH
  -NODAMAGETHRUST
  +FORCERADIUSDMG
  +NOEXTREMEDEATH
  +BLOODLESSIMPACT
  MissileType "Havoctrail"
  Deathsound "Weapons/Havokimpact"
  States
  {
    Spawn:
	  TNT1 A 4
	  Loop
    Death:
	  TNT1 A 0 Radius_Quake(6,6,0,12,0)
	  TNT1 A 0 A_Explode(20,40,1,1,20,0,0,"Apuff","havocsplash")
	  TNT1 A 0 A_JumpIf(GetCvar("Spooks_Toastermode")==1, "NOSFX")
	  TNT1 A 0 A_SpawnItemEx("Havocflare")
	NOSFX:
	  TNT1 A 1
      Stop
	}
}

ACTOR Havoctrailspawner
{
  +ThruSpecies
  +NoGravity
  +NoClip
  +NoDamageThrust
  Damagetype "Havoc"
  States
  {
    Spawn:
      TNT1 A 0
	  TNT1 A 0 A_Explode(2,60,1,1,40,0,0,"Apuff","havocstrailplash")
      TNT1 AAA 2 A_SpawnItemEx("HavocTrail", random (-3, 3), 0, random (-3, 3), 0, 0, 0, 0, 160)
      Stop
  }
}

Actor HavocTrail
{
  Species "Player"
  RenderStyle Add
  Scale 0.65
  Alpha 0.75
  +THRUSPECIES
  +ROLLSPRITE
  +NOINTERACTION
  +BRIGHT
  States
  {
  Spawn:
	MTS4 A 1 A_SetRoll(random(0,360))
	Goto SpawnLoop
  SpawnLoop:
	"----" A 1
		{
		A_FadeOut(0.33);
		A_SetRoll(random(0,360));
		}
	Loop
	}
}


ACTOR Havocflare
{
 +CLIENTSIDEONLY
 +NOINTERACTION
 +WALLSPRITE
 Scale 0.45
 Alpha 1.0
 Renderstyle Add
  States
{
  Spawn:
    MTSF D 2 Bright
  Continuous:
    MTSF D 1 Bright A_FadeOut(0.05)
    Loop
}}