ACTOR Tstrikebacon : CustomInventory
{
Inventory.amount 1
Inventory.MaxAmount 3
Inventory.Icon "ITM3E0"
Inventory.PickupMessage "UAC-brand 'Don't stand under this' \c[Rocketcolor]Tele-strike beacon\c- (Orbital bombardment)"
 Inventory.PickupSound "Beacon/Pickup"
 tag "$TSBeacon"
+INVBAR
States
{
  Spawn:
	TBEC A -1
    Stop
  Use:
	TNT1 A 1 A_SpawnItemEx("TStrikeBeacon",0,0,16,8,0,4,0)
	Stop
	}
}

Actor TStrikeBeacon
{
States
{
  Spawn:
    TBEC A 1
    TBEC A 20 Bright
	TBEC B 15 Bright A_PlaySound("ASmarker/on1",255)
	TBEC CDEFG 15 Bright Light("TSBeacon1")
	TBEC H 1 Bright A_PlaySound("ASmarker/on2",255)
	TBEC IJKL 1 Bright Light("TSBeacon1")
	TBEC M 1 Bright A_CustomMissile("TeleportedStrike")
	TBEC M 160 Bright Light("TSBeacon2") 
	TNT1 A 0
	TNT1 A 0 A_SpawnItemEx("OrangeParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128) 
    TNT1 AAA 0 A_CustomMissile("Kindasmallkaboom", 0, 0, random(0,360), 2, random(0,360))
	TNT1 AAA 0 A_CustomMissile("SmokeFX2", 0, 0, random(0,360), 2, random(0,360))
    TNT1 AAAA 0 A_SpawnItemEx("Debris",0,0,0,Random(4,-4),Random(4,-4),Random(4,8),0,128)
    TNT1 A 0 A_PlaySound("weapons/rocklx")
    TNT1 A 1 A_SpawnItem("RocketEx")
    Stop	
	}
}

ACTOR TeleportedStrike
{
Height 2
Radius 2
Speed 0
RenderStyle Add
Alpha 0.9
XScale 3
YScale 0.5
PROJECTILE
+SPAWNCEILING
+CEILINGHUGGER
+NOCLIP
+DONTSPLASH
var int user_rocketloop;
    States
    {
    Spawn:
        TNT1 A 35
    SpawnLoop:
        TNT1 A 0 A_JumpIf(user_rocketloop >=20,"Null")
        TNT1 A 8
        {
            A_SpawnItemEx("TeleportedBomb",random(-200,200),random(-200,200),-18);
            A_SetUserVar(user_rocketloop,user_rocketloop+1);
        }
        loop
	}
}

ACTOR TeleportedBomb
{
Radius 8
Height 8
Speed 0
Damage (100)
Scale 1.3
PROJECTILE
+SPAWNCEILING
+DONTSPLASH
+FORCERADIUSDMG
-NOGRAVITY
-FLOAT
SeeSound "rocket/fire"
DeathSound "Rocket/boom"
Obituary " %o thought standing inside of the blast radius of a Tele-strike beacon was a smart idea."
States
{
  Spawn:
		TNT1 A 0 Nodelay A_SpawnItemEx("TeleportFog")
   Fly:
       TNT1 A 0 A_JumpIf(GetCvar("Spooks_Toastermode")==1, "Reducedfly")
   Standard:
		TBOM AAAAAAAAA 1 Bright A_SpawnItemEx("RMissiletrail",10,0,40,0,0,0)
		Goto Fly
	Reducedfly:
		TBOM A 1 bright
		Goto fly
	Death:
		TNT1 A 0 A_JumpIf(GetCvar("Spooks_Toastermode")==1, "Lessexplode")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SpawnItemEx("OrangeParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128) 
		TNT1 AAA 0 a_custommissile("SmallKaboom", 0, 0, random(0,360), 2, random(0,360))
		TNT1 A 1 A_SpawnItem("RocketEx")
		TNT1 A 0 A_Explode(150,150,xf_notmissile,1,130,0,0,"Apuff","Rocketkaboom")
		TNT1 A 0 Radius_quake(3,8,0,10,0)
		TNT1 A 0 Radius_Quake(1,8,0,28,0)
		TNT1 AAAA 0 
		{
		A_SpawnItemEx("Debris",0,0,0,FRandom(4,-4),FRandom(4,-4),FRandom(4,8),0,128);
		A_SpawnItemEx("smokeFX2", random(13,-13), random(13,-13), random(13,-13), 0.1*random(2,5), 0.1*random(2,5), 0.1*random(2,5), random(0,360), 128, 0);
		}
		TNT1 AAAAA 5 A_SpawnItemEx("smokeFX2", random(13,-13), random(13,-13), random(13,-13), 1*random(2,5), 1*random(2,5), 1*random(2,5), random(0,360), 128, 0)
		Stop
	LessExplode:
		TNT1 A 1 A_SpawnItem("RocketEx")
		TNT1 A 0 Radius_quake(3,8,0,10,0)
		TNT1 A 0 Radius_Quake(1,8,0,28,0)
		TNT1 A 0 A_Explode(150,150,xf_notmissile,1,130,0,0,"Apuff","Rocketkaboom")
		Stop
    }
}