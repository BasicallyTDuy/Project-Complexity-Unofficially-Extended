//Pickable item and the beacon itself
Actor DSbaconitem : CustomInventory
{
Height 10
Radius 10
+COUNTITEM
+INVENTORY.INVBAR
Inventory.Amount 1
Inventory.MaxAmount 3
Inventory.Icon "BEACX0"
 Inventory.PickupSound "Beacon/Pickup"
Inventory.PickupMessage "UAC-Brand 'Holy fuck is that a wall of projectiles?', \c[Godspheregreen]Shield Beacon\c- (Deploys a shield.)"
Tag "$Sbeacon"
States
	{
	Spawn:
		DEPS A -1
		Stop
	Use:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(waterlevel > 2,"Nope")
		TNT1 A 0 A_playsound("Beacon/place",7)
		TNT1 A 1 A_SpawnItemEx("Shieldbeacon",0,0,8,0,0,0,0,SXF_NOCHECKPOSITION,0)
		Stop
	 Nope:
		TNT1 A 0 
		Fail
	}
}

Actor Shieldbeacon
{
-SOLID
-Shootable
mass 0x7fffffff
Var int User_shieldcount;
States
	{
	Spawn:
		DEPS A 35
		DEPS DCB 10
		TNT1 A 0 A_PlaySound ("ForceBarrier/On",5,1)
	Idle1:
		TNT1 A 0 A_JumpIf(user_shieldcount==30,"Idle2")
		TNT1 A 0 A_PlaySound ("ForceBarrier/Loop",7,1,1)
		DEPS BBBBBBBBBBBB 3 bright Light("CBeaconB") A_SpawnItemEx ("Firststageshield",10,0,10)
		TNT1 A 0 A_SetUserVar("user_shieldcount",user_shieldcount+1)
		Loop
	Idle2:
		TNT1 A 0 A_SetUserVar("user_shieldcount",0) 		
	Idle2loop:
		TNT1 A 0 A_JumpIf(user_shieldcount==20,"Idle3")
		DEPS CCCCCCCCCCCC 3 Bright Light("Dbeacon") A_SpawnItemEx ("Firststageshield",10,0,10)
		TNT1 A 0 A_SetUserVar("user_shieldcount",user_shieldcount+1)
		Loop	
	Idle3:
		TNT1 A 0 A_SetUserVar("user_shieldcount",0) 		
	Idle3loop:
		TNT1 A 0 A_JumpIf(user_shieldcount==20,"Final")
		DEPS DDDDDDDDDDD 3 Bright Light("Ebeacon") A_SpawnItemEx ("Firststageshield",10,0,10)
		TNT1 A 0 A_SetUserVar("user_shieldcount",user_shieldcount+1)
		Loop	
	Final:
		TNT1 A 0 A_SetUserVar("user_shieldcount",0) 		
	FinalLoop:
		TNT1 A 0 A_JumpIf(user_shieldcount==10,"Death")
		DEPS EEEEEEEEEEEE 3 Bright Light("AbeaconB") A_SpawnItemEx ("Firststageshield",10,0,10)
		TNT1 A 0 A_SetUserVar("user_shieldcount",user_shieldcount+1)
		Loop	
	death:
		TNT1 A 0 A_stopsound(7)
		DEPS A 105 Bright  A_playsound("Dispenser/PDown",7)
		TNT1 A 0 A_playsound("world/barrelx",7)
		TNT1 A 0 A_JumpIf(GetCvar("Spooks_Toastermode")==1, "Lessexplode")
		TNT1 A 0 A_SpawnItemEx("OrangeParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128) 
		TNT1 AAA 0 a_custommissile("SmallKaboom", 0, 0, random(0,360), 2, random(0,360))
		TNT1 AAAAAAAA 0 A_SpawnItemEx("MineExSpark",0,0,10,FRandom(6,-6),FRandom(6,-6),FRandom(8,12))
		TNT1 A 1 A_SpawnItemEx("MineEx",0,0,0,0,0,0,0,128)
		TNT1 A 0 Radius_quake(3,8,0,10,0)
		TNT1 A 0 Radius_Quake(1,8,0,28,0)
		TNT1 AAAA 0 
			{
			A_SpawnItemEx("smokeFX2", random(13,-13), random(13,-13), random(13,-13), 0.1*random(2,5), 0.1*random(2,5), 0.1*random(2,5), random(0,360), 128, 0);
			A_SpawnItemEx("Debris",0,0,0,FRandom(4,-4),FRandom(4,-4),FRandom(4,8),0,128);
			}
		TNT1 AAAAA 5 A_SpawnItemEx("smokeFX2", random(13,-13), random(13,-13), random(13,-13), 1*random(2,5), 1*random(2,5), 1*random(2,5), random(0,360), 128, 0)
		TNT1 A -1 a_noblocking
		stop
	LessExplode:
		TNT1 A 0 A_playsound("world/barrelx",7)
		TNT1 A 1 A_SpawnItemEx("MineEx",0,0,0,0,0,0,0,128)
		TNT1 A 0 Radius_quake(3,8,0,10,0)
		TNT1 A 0 Radius_Quake(1,8,0,28,0)
		TNT1 A -1 a_noblocking
		stop
	}
}

//Actual Shield stuff
//GREEN SHIELD

Actor Firststageshield
{
  Radius 1
  Height 1
  +NOGRAVITY
  +NOINTERACTION
  States
  {
  Spawn:
	TNT1 A 1 Nodelay
		{
		A_SpawnItemEx ("PShieldSpawner5up", 8, -96, 0);
		A_SpawnItemEx ("PShieldSpawner5up", 8, -64, 0);
		A_SpawnItemEx ("PShieldSpawner5up", 8, -32, 0);
		A_SpawnItemEx ("PShieldSpawner5up", 8, 0, 0);
		A_SpawnItemEx ("PShieldSpawner5up", 8, 32, 0);
		A_SpawnItemEx ("PShieldSpawner5up", 8, 64, 0);				
		A_SpawnItemEx ("PShieldSpawner5up", 8, 96, 0);		
		}
	stop
  }
}

actor PShieldPartVisual
{
  Radius 8
  Height 8
  Scale 0.5
  RenderStyle Add	//Fuzzy looks great in software mode
  Alpha 0.25
  +NOINTERACTION
  +CLIENTSIDEONLY
  +FLOORCLIP
  +WALLSPRITE
  States
  {
  Spawn:
    CAFX A 4 bright
    stop
  Death:
    TNT1 A 2
    stop
  }
}

actor PShieldSpawner5up
{
  Radius 1
  Height 1
  +NOGRAVITY
  +NOINTERACTION
  States
  {
  Spawn:
    TNT1 A 1 Nodelay 
		{
		A_SpawnItemEx ("PShieldPart", 0, 0, 0, 0, 0, 0, 0, 32);
		A_SpawnItemEx ("PShieldPart", 0, 0, 16, 0, 0, 0, 0, 32);
		A_SpawnItemEx ("PShieldPart", 0, 0, 32, 0, 0, 0, 0, 32);
		A_SpawnItemEx ("PShieldPart", 0, 0, 48, 0, 0, 0, 0, 32);
		A_SpawnItemEx ("PShieldPart", 0, 0, 64, 0, 0, 0, 0, 32);
		A_SpawnItemEx ("PShieldPart", 0, 0, 80, 0, 0, 0, 0, 32);
		A_SpawnItemEx ("PShieldPartVisual", 0, 0, 0, 0, 0, 0, 0, 32);
		A_SpawnItemEx ("PShieldPartVisual", 0, 0, 16, 0, 0, 0, 0, 32);
		A_SpawnItemEx ("PShieldPartVisual", 0, 0, 32, 0, 0, 0, 0, 32);
		A_SpawnItemEx ("PShieldPartVisual", 0, 0, 48, 0, 0, 0, 0, 32);
		A_SpawnItemEx ("PShieldPartVisual", 0, 0, 64, 0, 0, 0, 0, 32);
		A_SpawnItemEx ("PShieldPartVisual", 0, 0, 80, 0, 0, 0, 0, 32);
		}
	stop
  }
}

actor PShieldHit
{
  Radius 1
  Height 1
  Scale 0.5
  Renderstyle Add
  +NOGRAVITY
  +NOINTERACTION
  States
  {
  Spawn:
    TNT1 A 0 Nodelay A_startsound("ForceBarrier/Hit",42060,0,1)
    FHIT BCDEFGH 1 bright
    stop
  }
}

actor PShieldPart
{
  Radius 32
  Height 32
  Scale 0.5
  RenderStyle None
  Alpha 0.25
  Species "Player"
  Bloodtype "PShieldHit"
  +FORCEPAIN
  +THRUSPECIES
  +MTHRUSPECIES
  +SHOOTABLE
  +NOGRAVITY
  +NOTELEPORT
  +NODAMAGE
  +NORADIUSDMG
  +DONTRIP
  +NOBLOODDECALS
  +FLOORCLIP
  +FLATSPRITE
  States
  {
  Spawn:
    HEXA A 4 bright
    stop
  Death:
    TNT1 A 2
    stop
  }
}

