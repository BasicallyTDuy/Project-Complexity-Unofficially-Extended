class doubletube : Ammo
{
	Default
	{
	  Inventory.Amount 0;
	  Inventory.MaxAmount 2;
	  Ammo.BackpackAmount 0;
	  Ammo.BackpackMaxAmount 2;
	  Inventory.Icon "SHELLS";
	  +INVENTORY.IGNORESKILL;
  }
}

class SSGSpawner : Actor
{
	Default
	{
		+FLOORCLIP;
	}
States
    {
    Spawn:
        TNT1 A 0 Nodelay
            {
                If (ACS_NamedExecuteWithResult("WeaponSwap",2) ==2)
                {
                  A_SpawnItemEx("BoxoShells", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERSPECIAL, 0, tid);
                  }
                Else
                  {
                  A_SpawnItemEx("JustaSSG", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERSPECIAL, 0, tid);
                  }
            }    
        Stop;
    }
}

class MapSSGSpawner : Actor
{
	Default
	{
		+FLOORCLIP;
	}
States
    {
    Spawn:
        TNT1 A 0 Nodelay
            {
                If (ACS_NamedExecuteWithResult("WeaponSwap",2) ==2)
                {
                  A_SpawnItemEx("BoxoShells", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERSPECIAL, 0, tid);
                  }
                Else
                  {
                  A_SpawnItemEx("JASSGspawner", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERSPECIAL, 0, tid);
                  }
            }    
        Stop;
    }
}

class JustaSSG : JustaWeapon
{
	Default
	{
		Weapon.AmmoType1 "doubletube";
		Weapon.AmmoUse1 0;
		Weapon.AmmoGive1 0;
		Weapon.AmmoType2 "Shells";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 6;
		Weapon.slotnumber 3;
		Weapon.SelectionOrder 400;
		Weapon.SlotPriority 60;
		Inventory.AltHUDIcon "SGN2A0";
		Obituary "%k Realized that double-barrel shotguns tend to fuck things up.";
		Inventory.PickupSound "weapons/shellslide";
		Inventory.Pickupmessage "[3] - \c[brighterOrange]Double-barreled shotgun\c- acquired.";
		Tag "$PC_SSGTAG";
	}
	
	action void PC_FireSSG(bool both)
	{
		PC_PlayPowerModSounds();
		A_AlertMonsters();
		
		if(both)
		{
			A_startsound("weapons/SSGDfire",10,0);
			PC_FireBullets("PC_SSGProjectile", 18, 4.2, 0, 0, 4.2,"DoubleTube",2,1);
			PC_QuakeCamera(3,6);
		}
		else
		{
			A_startsound("Weapons/SSGSFire",10,0);
			PC_FireBullets("PC_SSGProjectile", 9, 2.1, 0, 0, 2.1,"DoubleTube",1,1);
			PC_QuakeCamera(2,4);
		}
		
		A_FireCustomMissile("GSSpawner",0,0,0,4);
		A_ZoomFactor(0.99,ZOOM_INSTANT);
		
		A_GunFlash();
	}
	
	States
	{
		Spawn:
			SGN2 A -1;
			Stop;
		Select:
			SHTM BA 1 A_WeaponReady(WRF_NOFIRE);
			SHTA C 1 A_WeaponReady(WRF_NOFIRE);
			Goto ready;
		Deselect:
			SHTA C 1;
			SHTM AB 1;
			TNT1 A 0 A_lower;
			wait;
		Ready:
			SHTA C 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_ALLOWUSER1|WRF_ALLOWUSER2);
			Loop;
		Dryfire:
			SHTA C 16 A_PlaySound("Weapons/MGSGEmpty");
			SHTA C 4 A_jumpifinventory("Shells",1,1);
			Goto Reload;
			SHTA C 1;
			Goto Ready;
		Fire:
			TNT1 A 0 A_JumpIfInventory("Doubletube",2,"FireBoth");
			TNT1 A 0 A_JumpIfInventory("Shells",2,"Reload");
			Goto dryfire;
		FireBoth:
			SHO9 A 2 Bright
			{
				PC_FireSSG(true);
			}
			TNT1 A 0 A_JumpIf(GetCvar("Spooks_Norecoil")==1, "Followthrough");
			TNT1 A 0 PC_WeaponRecoil(6,6);
		Followthrough:
			SHO9 B 2 Bright;
			SHO8 C 1 A_ZoomFactor(0.997);
			SHO8 D 1 A_ZoomFactor(1.0);
			SHO8 EF 1;
			SHTA C 1;
			Goto Ready;
		Altfire:
			TNT1 A 0 A_JumpIfInventory("Doubletube",2,"FireRight");
			TNT1 A 0 A_JumpIfInventory("Doubletube",1,1);
			Goto dryfire;
		Fireleft:
			SHTA B 2 BRIGHT
			{
				PC_FireSSG(false);
			}
			TNT1 A 0 A_JumpIf(GetCvar("Spooks_Norecoil")==1, "FTLeft");
			TNT1 A 0 PC_WeaponRecoil(4,4);
		FTLeft:
			SHO8 C 1 A_ZoomFactor(0.999);
			SHO8 D 1 A_ZoomFactor(1.0);
			SHO8 EF 1;
			SHTA C 1;
			Goto Ready;
		Fireright:
			TNT1 A 0 A_JumpIfInventory("Doubletube",1,1);
			Goto dryfire;
			SHTA A 2 bright
			{
				PC_FireSSG(false);
			}
			TNT1 A 0 A_JumpIf(GetCvar("Spooks_Norecoil")==1, "FTRight");
			TNT1 A 0 PC_WeaponRecoil(4,4);
		FTRight:
			SHO8 C 1 A_ZoomFactor(0.999);
			SHO8 D 1  A_ZoomFactor(1.0);
			SHO8 EF 1;
			SHTA C 1;
			Goto Ready;
		Reload:
			TNT1 A 0 A_Jumpifinventory("Doubletube",2,"Ready");
			TNT1 A 0 A_Jumpifinventory("Doubletube",1,"Reloadright");
			TNT1 A 0 A_Jumpifinventory("Shells",2,"Doublereload");
			SHT2 A 1;
			Goto reloadright;
		Doublereload:
			//SHT2 BC 1 //[CHANGED]
			SHT2 BCDE 1;
			SHT2 F 1 A_startsound("Weapons/Sawedopen",9,0);
			SHT2 GHIJK 1;
			TNT1 AA 0 A_FireCustomMissile("SSGSSpawner",-6,0,-19,-20,-14);
			SHT2 L 10;
			SSGR AWXYZ 1;
			SSRR ABC 1;
			TNT1 A 0
				{
				A_startsound("Weapons/Shellslide",99,0);
				A_GiveInventory("Doubletube",1);
				A_TakeInventory("Shells",1,TIF_NOTAKEINFINITE);
				}
			SSGR BCDEFGHIJK 1;
			TNT1 A 0
				{
				A_startsound("Weapons/Shellslide",99,0);
				A_GiveInventory("Doubletube",1);
				A_TakeInventory("Shells",1,TIF_NOTAKEINFINITE);
				}
			SSGR NOPQ 1;
			TNT1 A 0 A_startsound("Weapons/Sawedclose",11,0);
			SHT2 TUVWX 1;
			SHT2 YZ 1;
			SHT2 EDCB 1;
			SHT2 A 1;
			Goto Ready;
		Reloadright:
			TNT1 A 0 A_Jumpifinventory("Shells",1,2);
			SHT2 A 1;
			Goto Ready;
			//SHT2 BC 1 //[CHANGED]
			SHT2 BCDE 1;
			SHT2 F 1 A_startsound("Weapons/Sawedopen",9,0);
			SHT2 GHIJK 1;
			TNT1 A 0 A_FireCustomMissile("SSGSSpawner",-6,0,-19,-20,-14) ;
			SHT7 L 10;
			SSGR RSTUVMNOPQ 1;
			TNT1 A 0
				{
				A_startsound("Weapons/shellslide",99,0);
				A_GiveInventory("Doubletube",1);
				A_TakeInventory("Shells",1,TIF_NOTAKEINFINITE);
				}
			TNT1 A 0 A_startsound("Weapons/Sawedclose",11,0);
			SHT2 TUVWX 1;
			SHT2 YZ 1;
			SHT2 EDCB 1;
			SHT2 A 1;
			Goto Ready;
		User1:
			TNT1 A 0 A_JumpIfInventory("HGammo",1,1);
			Goto Ready;
			SHTA C 1;
			SHTM AB 1;
			Goto nadethrow;
		User2:
			TNT1 A 0 A_JumpIfInventory("Mineammo",1,1);
			Goto Ready;
			SHTA C 1;
			SHTM AB 1;
			Goto Minethrow;
		Returnstate:
			SHTM BA 1;
			SHTA C 1;
			Goto Ready;
	}
}