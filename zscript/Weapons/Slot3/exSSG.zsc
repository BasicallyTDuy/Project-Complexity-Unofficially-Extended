class Boomertube : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 6;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 6;
		Inventory.Icon "XSHELLS";
		+INVENTORY.IGNORESKILL;
	}
}

class Eshotguntoken : Inventory{ Default { Inventory.maxamount 1; }}
class ESSGdrop : Actor
{
	Default
	{
		+FLOORCLIP
	}
States
    {
    Spawn:
        TNT1 A 0 Nodelay
            {
                If (ACS_NamedExecuteWithResult("WeaponSwap",5) ==2)
                {
                  A_SpawnItemEx("Eshellbox", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERSPECIAL, 0, tid);
                  }
                Else
                  {
                  A_SpawnItemEx("ESSGPickup", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERSPECIAL, 0, tid);
                  }
            }    
        Stop;
    }
}

/*Actor ESSGPickup : Custominventory
{
    Inventory.PickupSound "weapons/shellslide"
	Inventory.Pickupmessage "[3] - \c[Explosiveorange]Explosive Shotvolver\c- acquired."
	States
	{
		spawn:
			XREV Z -1
			Stop
		Pickup:
			TNT1 A 0 A_jumpifinventory("EShells",0,"Nopickup")
			TNT1 A 0
				{
				A_Giveinventory("Eshotguntoken",1);
				A_Giveinventory("JustaESSG",1);
				A_Takeinventory("JustaESG",1);
				}
			Stop
		Nopickup:
			TNT1 A 0
			Fail
	}
}*/

class JustaESSG : JustaWeapon
{
	Default
	{
		Weapon.AmmoType1 "Boomertube";
		Weapon.AmmoUse1 0;
		Weapon.AmmoGive1 0;
		Weapon.AmmoType2 "EShells";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 6;
		Weapon.SelectionOrder 500;
		Weapon.Slotnumber 3;
		Weapon.SlotPriority 20;
		Scale 0.9;
		Obituary "%k was smeared across. . . well, everything, by a literal handcannon.";
		Weapon.UpSound "Weapon/Select";
		Inventory.PickupSound "weapons/shellslide";
		Inventory.Pickupmessage "[3] - \c[Explosiveorange]Explosive Shotvolver\c- acquired.";
		Tag "$PC_EXPLOSIVESHOTVOLVERTAG";
		Inventory.AltHUDIcon "XREVZ0";
	}
	
	
	States
		{
		spawn:
			REVP A -1;
			Stop;
		Select:
			REVO CB 1 A_WeaponReady(WRF_NOFIRE);
			REVO A 1 A_WeaponReady(WRF_NOFIRE);
			Goto ready;
		Deselect:
			REVO A 1;
			REVO BC 1;
			TNT1 A 0 A_lower;
			wait;
		Ready:
			REVO A 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_ALLOWRELOAD|WRF_ALLOWUSER1|WRF_ALLOWUSER2);
			Loop;
		Dryfire:
			REVF K 4 A_startsound("Weapons/RevolverEmpty",11);
			REVF LMNOPQRSTUVW 1;
			Goto ready;
		Fire:
			TNT1 A 0 A_Jumpifinventory("Boomertube",1,1);
			Goto Dryfire;
			REVF K 1 A_startsound("Weapons/RevolverEmpty",11);
			TNT1 A 0
				{
				A_AlertMonsters();
					If(CountInv("Power4xDamage"))
					{
						 A_startsound("Weapons/quadfire",5);
					}
					If(CountInv("Chalicedamage"))
					{
						 A_startsound("Weapons/quadfire",5);
					}
				 A_startsound("Weapons/ESSGfire",10,0,1);
				A_GunFlash();
				}	
			TNT1 A 0 A_FireBullets(3, 3, -1, 200, "ESSGPuff",FBF_NORANDOM);
			TNT1 A 0 
				 {
				  if(CountInv("Guntoken") && random(1,5) == 5)
					{
					A_FireBullets(6, 6, -1, 200, "ESSGPuff",FBF_NORANDOM);
					A_startsound("Gun/Proc",999,0,1);
					}
				  }
			REVF BC 1 BRIGHT;
			TNT1 A 0 PC_QuakeCamera(2,4); //Radius_Quake(3,3,0,1,0)
			TNT1 A 0 A_ZoomFactor(0.99,ZOOM_INSTANT);
			TNT1 A 0 A_JumpIf(GetCvar("Spooks_Norecoil")==1, "Followthrough");
			TNT1 A 0 PC_WeaponRecoil(8,8);
		Followthrough:
			TNT1 A 0 A_ZoomFactor(0.999);
			REVF DEFG 1; //A_FireCustomMissile("GSSpawner",0,0,0,4);
			TNT1 A 0 A_ZoomFactor(1.0);
			REVF HIJKLMNO 1;
			TNT1 A 0 A_startsound("Weapons/ESSGHammer",12);
			TNT1 A 0 
				 {
					if(CountInv("Ammotoken") && random(1,5) == 5) 
					{
					A_startsound("ammo/Proc",999,0,0.65);
					}
				else
					{
					A_takeinventory("boomertube",1,TIF_NOTAKEINFINITE); 
					}
				}
			REVF PQRSTUVW 1;
			Goto ready;
		Reload:
			TNT1 A 0 A_JumpIfInventory("Boomertube", 6, 2);
			TNT1 A 0 A_JumpIfInventory("EShells", 1, "Properreload");
			REVO A 1;
			Goto Ready;
		Properreload:
			REVR ABCDEF 1;
			TNT1 A 0 A_startsound("Rev/open",9);
			REVR GHIJKLMNOPQ 1;
			REVR Q 5;
		Reloadloop:
			REVR WVUT 1;
		    TNT1 A 0 A_playsound("weapons/shellslide",6);
			REVR SR 1;
			REVR Z 1;
			TNT1 A 0 A_SpawnItemEx("SpentExpshellcasing",0,0,20,random(-1,1),random(-1,1),0,random(1,359),SXF_NOCHECKPOSITION);
			REVR Z 5;
			REVR RS 1;
			TNT1 A 0 
				{
				A_startsound("Weapons/ExShellsert",14);
				A_TakeInventory("EShells",1);
				A_GiveInventory("Boomertube",1);
				}
			REVR TUVW 1;
			TNT1 A 0 A_startsound("Weapons/Revspin",13);
			REVR XYQ 1;
			REVR Q 5 A_WeaponReady(WRF_NOBOB|WRF_ALLOWUSER1|WRF_ALLOWUSER2|WRF_ALLOWZOOM);
			TNT1 A 0 A_JumpIfInventory("Boomertube",6,"ReloadFinish");
			TNT1 A 0 A_JumpIfInventory("EShells",1,"Reloadloop");
			Goto ReloadFinish;
		Reloadfinish:
			REVR QPONMLKJI 1;
			TNT1 A 0 A_startsound("Rev/close",16);
			REVR HGFEDCBA 1;
			//TNT1 A 0 A_startsound("Weapons/ESSGHammer",18);
			REVO A 4 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWUSER1|WRF_ALLOWUSER2);
			Goto Ready;
		Zoom:
			REVO A 1;
			REVO BC 1;
			Goto Kicking;
		User1:
			TNT1 A 0 A_JumpIfInventory("HGammo",1,1);
			Goto Ready;
			REVO A 1;
			REVO BC 1;
			Goto nadethrow;
		User2:
			TNT1 A 0 A_JumpIfInventory("Mineammo",1,1);
			Goto Ready;
			REVO A 1;
			REVO BC 1;
			Goto Minethrow;
		Returnstate:
			REVO CB 1;
			REVO A 1;
			Goto Ready;
		}
}
			