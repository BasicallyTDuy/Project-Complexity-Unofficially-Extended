class JustaGL : JustaWeapon
{
	Default
	{
		Weapon.AmmoType2 "C4Ammo";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 2;
		Obituary "%k didn't realized non-launched rockets were dangerous.";
		Inventory.PickupSound "weapons/shellslide";
		weapon.slotnumber 6;
		Weapon.SelectionOrder 2300;
		Weapon.SlotPriority 20;
		Inventory.AltHUDIcon "GLAPA0";
		Inventory.pickupmessage "[6] - \c[Rocketcolor]Pipebomb launcher\c- acquired. ";
		Tag "$PC_GRENADELAUNCHERTAG";
	}
	
	action void PC_FireGL()
	{
		PC_PlayPowerModSounds();
		
		A_ZoomFactor(0.999);
		A_FireCustomMissile("GSSpawner2",0,0,0,8);
		A_startsound("Weapons/GLfire",10);
		A_AlertMonsters(100);
		
		A_FireCustomMissile("ThrownPipebombs",0,0,0,0,0,6);
		
		if(CountInv("Guntoken") && random(1,5) == 5)
		{
			A_FireCustomMissile("ThrownPipebombs",0,0,0,0,0,6);
			A_startsound("Gun/Proc",999,0,1);
		}
		
		if(CountInv("Ammotoken") && random(1,5) == 5) 
		{
			A_startsound("ammo/Proc",999,0,0.65);
		}
		else
		{
			A_Takeinventory("C4Ammo",1,TIF_NOTAKEINFINITE);
		}
	}
	
	States
	{
		Spawn:
			GLAP A -1;
			Stop;
		Select: 
			GNAM BA 1 A_WeaponReady(WRF_NOFIRE);
			GLAN A 1 A_WeaponReady(WRF_NOFIRE);
			Goto ready;
		Deselect: 
			GLAN A 1;
			GNAM AB 1;
			TNT1 A 0 A_lower;
			wait;
		Ready:
			GLAN A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM|WRF_ALLOWUSER1|WRF_ALLOWUSER2);
			Loop;
		Zoom:
			GLAN A 1;
			Goto kicking;
		Dryfire:
			GLAN A 16 A_startsound("Weapons/Grenadeempty",11);
			GLAN A 4;
			Goto ready;
		Fire:
			TNT1 A 0 A_jumpifinventory("C4Ammo",1,1);
			goto Dryfire;
			GLAF X 2 BRIGHT 
			{
				A_GunFlash();
				PC_FireGL();
			}
			GLAF A 2 bright A_SetPitch(pitch-5, SPF_INTERPOLATE);
			GLAF EEDCB 1 A_SetPitch(1 + pitch, SPF_INTERPOLATE);
			TNT1 A 0 A_ZoomFactor(1.0);
			GLAN A 4;
			TNT1 A 0 A_JumpIf(CountInv("Rockets") == 0,"Ready");
			GLR1 DEF 1;
			GLR1 G 2;
			GLR1 FHIJ 1;
			GLR1 K 2 A_startsound("Weapons/Grenadeopen",9);
			GLR1 LMNO 1;
			GLR1 P 4;
			GLR1 QRSTUV 1;
			GLR1 W 2 A_startsound("Weapons/Grenadeload",10);
			GLR1 XYZ 1;
			GLR2 A 1;
			TNT1 A 2;
			TNT1 A 0 A_startsound("Weapons/Grenadeclose",9);
			GLSA EDCB 1;
			GLAN A 1;
			Goto Ready;
		Altfire:
			GLAN A 1;
			GLR1 CDEF 1;
			TNT1 A 0 A_Overlay(4, "DetonatePipebombOverlay");
			GLR1 G 20;
			GLR1 FEDC 1;
			Goto Ready;
		User1:
			TNT1 A 0 A_JumpIfInventory("HGammo",1,1);
			Goto Ready;
			GLAN A 1;
			GNAM AB 1;
			Goto nadethrow;
		User2:
			TNT1 A 0 A_JumpIfInventory("Mineammo",1,1);
			Goto Ready;
			GLAN A 1;
			GNAM AB 1;
			Goto Minethrow;
		Returnstate:
			GNAM BA 1;
			GLAN A 1;
			Goto Ready;
		
		DetonatePipebombOverlay:
			TNT1 A 0
			{
				A_OverlayScale(4, -1, 1, 0);
				A_OverlayOffset(4, 320, 0);
			}
			DETO ABCDE 1;
			DETO E 2;
			DETO EDEFG 1;
			DETO G 1
			{
				A_GiveInventory("GrenadeDetonator", 1);
				A_startsound("Weapons/pipebutton",10);
			}
			DETO GFEDCBA 1;
			TNT1 A 0 A_TakeInventory("GrenadeDetonator", 1);
			Stop;
	}
}