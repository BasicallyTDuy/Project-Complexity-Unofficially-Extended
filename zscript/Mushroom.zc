Class Mushroomactor : Actor
{
void A_smartshroom(class<Actor> spawntype = "FatShot", int numspawns = 0, int flags = 0, double vrange = 4.0, double hrange = 0.5)
	{
		int i, j;

		if (numspawns == 0)
		{
			numspawns = GetMissileDamage(0, 1);
		}

		// Now launch mushroom cloud
		Actor aimtarget = Spawn("Mapspot", pos, NO_REPLACE);	// We need something to aim at.
		if (aimtarget == null) return;
		Actor owner = (flags & MSF_DontHurt) ? target : Actor (self);
		aimtarget.Height = Height;
		
		bool shootmode = ((flags & MSF_Classic) || // Flag explicitly set, or no flags and compat options
					(flags == 0 && CurState.bDehacked && (Level.compatflags & COMPATF_MUSHROOM)));

		for (i = -numspawns; i <= numspawns; i += 8)
		{
			for (j = -numspawns; j <= numspawns; j += 8)
			{
				Actor mo;
				aimtarget.SetXYZ(pos + (i, j, (i, j).Length() * vrange)); // Aim up fairly high
				if (shootmode)
				{	// Use old function for MBF compatibility
					mo = OldSpawnMissile(aimtarget, spawntype, owner);
				}
				else // Use normal function
				{
					mo = SpawnMissile(aimtarget, spawntype, owner);
				}
				if (mo)
				{	// Slow it down a bit
					mo.Vel *= hrange;
					mo.bNoGravity = false;   // Make debris fall under gravity
				}
			}
		}
		aimtarget.Destroy();
	}
	
}